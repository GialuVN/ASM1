;CHUONG TRINH DONG HO 6 SO
;/\/\.THI
;VINH LONG, 12/08/2010
;=======================
;dinh nghia
;DU LIEU
	DL	EQU	P0
;CHON LED
	SL  DATA P2
	L1	BIT	P2.0
	L2	BIT P2.1
	L3	BIT	P2.2
	L4	BIT P2.3
	L5	BIT P2.4
	L6  BIT	P2.5
;PHIM
	K1  BIT P3.0
	K2  BIT P3.1
;BIEN
	G1  DATA 30H ;CH  ;GIO
	G2  DATA 31H ;DV	
	PH1 DATA 32H ;CH  ;PHUT
	PH2 DATA 33H ;DV
	SE1 DATA 34H ;CH  ;GIAY
	SE2 DATA 35H ;DV

	COUT DATA 36H
	TEM DATA 20H
	FLAG BIT TEM.0
	KEP	BIT TEM.1



	ORG 00
	JMP BEGIN
	ORG 001BH
	MOV TH1,#HIGH(-49995)
	MOV TL1,#LOW(-49995)
	SETB FLAG
	RETI

BEGIN:
;KHOI TAO
	MOV TMOD,#10H	;TIMER1,CHE DO 16 BIT
	MOV IE,#88H		;CHO PHEP NGAT DO TIMER 1
	MOV DL,#255
	MOV SL,#255
	SETB K1
	SETB K2
	CLR KEP
	MOV G1,#0
	MOV G2,#0
	MOV PH1,#0
	MOV PH2,#0
	MOV SE1,#0
	MOV SE2,#0
	MOV COUT,#20
	SETB TR1
	SETB TF1
;CHUONG TRINH CHINH
MAIN:
		CALL TIMING
		CALL HT
		JNB K1,CH_GIO
		JNB K2,CH_PHUT
		JMP MAIN
;CHINH GIO
CH_GIO:
		INC G2
		CALL KT
		MOV A,G2
		CJNE A,#10,THOAT_CH_GIO
		MOV G2,#0
		INC G1
		
		
THOAT_CH_GIO:

		MOV 37H,#100
		MOV 38H,#20
CH_GIO2:		
		CALL TIMING
		CALL HT
		JNB KEP,CONT_G1
		DJNZ 38H,CH_GIO2
		JNB K1,CH_GIO
		JMP TCG1
CONT_G1:
		JB K1,TCG1
		DJNZ 37H,CH_GIO2
		SETB KEP
		JMP CH_GIO
TCG1:
		CLR KEP
		JMP MAIN
;CHINH PHUT
CH_PHUT:
		INC PH2
		MOV A,PH2
		CJNE A,#10,THOAT_CH_PHUT
		MOV PH2,#0
		INC PH1
		MOV A,PH1
		CJNE A,#6,THOAT_CH_PHUT
		MOV PH1,#0
THOAT_CH_PHUT:
		MOV 37H,#100
		MOV 38H,#10
CH_PHUT2:		
		CALL TIMING
		CALL HT
		JNB KEP,CONT_P1
		DJNZ 38H,CH_PHUT2
		JNB K2,CH_PHUT
		JMP TCP1
CONT_P1:
		JB K2,TCP1
		DJNZ 37H,CH_PHUT2
		SETB KEP
		JMP CH_PHUT
TCP1:
		CLR KEP
		JMP MAIN
;DEM THOI GIAN
DEM:
		INC SE2
		MOV A,SE2
		CJNE A,#10,THOAT_DEM
		MOV SE2,#0
		INC SE1
		MOV A,SE1
		CJNE A,#6,THOAT_DEM
		MOV SE1,#0
		INC PH2
		MOV A,PH2
		CJNE A,#10,THOAT_DEM
		MOV PH2,#0
		INC PH1
		MOV A,PH1
		CJNE A,#6,THOAT_DEM
		MOV PH1,#0
		INC G2
		MOV A,G2
		CJNE A,#10,THOAT_DEM
		MOV G2,#0
		INC G1
THOAT_DEM:
		CALL KT
		RET
;DEM TOI 24 GIO THI TRO VE 00
KT:
		MOV A,G1
		CJNE A,#2,THOAT_KT
		MOV A,G2
		CJNE A,#4,THOAT_KT
		MOV G1,#0
		MOV G2,#0
THOAT_KT:
		RET

;HIEN THI SO DEM LEN LED 7 DOAN
HT:
		MOV A,SE2
		CALL GMA
		MOV DL,A
		CLR L1
		CALL DELAY
		SETB L1
		MOV A,SE1
		CALL GMA
		MOV DL,A
		CLR L2
		CALL DELAY
		SETB L2
		MOV A,PH2
		CALL GMA
		MOV DL,A
		CLR L3
		CALL DELAY
		SETB L3
		MOV A,PH1
		CALL GMA
		MOV DL,A
		CLR L4
		CALL DELAY
		SETB L4
		MOV A,G2
		CALL GMA
		MOV DL,A
		CLR L5
		CALL DELAY
		SETB L5
		MOV A,G1
		CALL GMA
		MOV DL,A
		CLR L6
		CALL DELAY
		SETB L6
		RET
;GIAI MA SO DEM SANG MA LE 7 DOAN
GMA:
		MOV DPTR,#MA
		MOVC A, @A+DPTR
		RET
;SAU 20X50000 THI TANG LEN 1 GIAY
TIMING:
		JNB FLAG,EXT_TIMING
		CLR FLAG
		DJNZ COUT,EXT_TIMING
		MOV COUT,#20
		CALL DEM
		CPL P3.7
EXT_TIMING:
		RET
;DELAY
DELAY:
		MOV R7,#10
REP_DELAY:
		MOV R6,#100
		DJNZ R6,$
		DJNZ R7,REP_DELAY
		RET
MA:
		DB 0C0H, 0F9H, 0A4H, 0B0H, 99H, 92H, 82H, 0F8H, 80H, 90H
		END
