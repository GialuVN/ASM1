;====================================================================
;===CHUYEN DOI SO NHI PHAN 16BIT(0 T0 65535) - THAP PHAN DANG BCD====
;==========================LU MINH THI ==============================
;==========================13/03/2010================================
;=======================SPKT VING LONG===============================
;====================================================================
;KHAI BAO BIEN

_BL DATA 30H		;BYTE CAO
_BH DATA 31H		;BYTE THAP
_D1 DATA 32H		;CHUC|DON VI
_D2 DATA 33H		;NGAN|TRAM
_D3 DATA 34H		;   0|CHUC NGAN
_SL DATA 35H
_SH DATA 36H
;BAT DAU CHUONG TRINH
ORG 0000H
MAIN:
CALL CLR_DEC
MOV _SL,#0
MOV _SH,#0
SMAIN:

MOV _BL,_SL
MOV _BH,_SH
CALL BIN_BCD
MOV P1,_D1
MOV P2,_D2
MOV P3,_D3
CALL CLR_DEC
CALL DELAY
MOV A,#2
CALL CONG
JMP SMAIN
;=============CHUYEN SO NHI PHAN 16BIT -  SO BCD=====================
BIN_BCD:
		PUSH 00H
		PUSH 01H
		PUSH 02H
		PUSH 03H
		MOV R2,#2					;NAP 2 VAO R2
LOOP_BIN_BCD:
		CALL SHIFT					;DICH SANG TRAI 1 BIT CUA 5 BYTE
		DJNZ R2,LOOP_BIN_BCD		;DICH SANG TRAI 2 BIT CUA 5 BYTE
		MOV R0,#14					;NAP 14 VAO R0
LOOP3_BIN_BCD:		
		CALL SHIFT					;DICH SANG TRAI 1 LAN NUA
		CJNE R0,#1,CONT_BIN_BCD		;DICH DUOC 16 LAN CHUA?
		JMP END_BIN_BCD				;THOAT KHOI CHUONG TRINH BIN_BCD
CONT_BIN_BCD:
		MOV R1,#32H					;NAP 32H VAO R0
LOOP2_BIN_BCD:
		MOV A,@R1					;LAY NOI DUNG TAI DIA CHI 32H NAP VAO A
		PUSH ACC					;CAT THANH CHUA A
		ANL A,#0FH					;XOA 4 BIT CAO
		MOV R3,A					;CAT VAO R3
		ADD A,#251					;CONG A VOI 251 (NEU A>5, THI C=1 NGUOC LAI C=0)
		JNC EXT1_BIN_BCD			;NHAY NEU C=1
		MOV A,R3					;NAP R3 LAI CHO A
		ADD A,#3					;CONG A VOI 3
		MOV R3,A					;NAP A CHO R3
EXT1_BIN_BCD:
		POP ACC						;LAY LAI NOI DUNG THANH CHUA A (MOV A,@R1)
		SWAP A						;DAO 4BIT THAP VA CAO
		ANL A,#0FH					;XOA 4 BIT CAO
		MOV R2,A					;NAP A VAO R2
		ADD A,#251					;CONG A VOI 251 (NEU A>5, THI C=1 NGUOC LAI C=0)
		JNC EXT2_BIN_BCD			;NHAY NEU C=1
		MOV A,R2					;NAP R2 VAO A
		ADD A,#3					;CONG A VOI 3
		MOV R2,A					;NAP A VAO R2
EXT2_BIN_BCD:
		MOV A,R2					;NAP R2 VAO A
		SWAP A						;DAO 4 BIT THAP VA CAO
		ORL A,R3					;ORL R3 VOI A
		MOV @R1,A					;CAT TRO LAI
		INC R1
		CJNE R1,#35H,LOOP2_BIN_BCD
		DJNZ R0,LOOP3_BIN_BCD
END_BIN_BCD:
		POP 03H
		POP 02H
		POP 01H
		POP 00H
		RET
;==============DICH SANG TRAI 1BIT CUA 5 BYTE===============
SHIFT:
		PUSH 01H
		PUSH 00H
		MOV R0,#30H
		MOV R1,#5
		CLR C						;XOA CO C
LOOP_SHIFT:
		MOV A,@R0					
		RLC A
		MOV @R0,A
		INC R0
		DJNZ R1,LOOP_SHIFT
		POP 00H
		POP 01H
		RET
;============XOA SO THAP PHAN TRONG CAC THANH GHI===========
CLR_DEC:
		PUSH 00H
		MOV R0,#32H
LOOP_CLR_DEC:
		MOV @R0,#0
		INC R0
		CJNE R0,#35H,LOOP_CLR_DEC
		POP 00H
		RET
;====================16BIT+8BIT============================
CONG:
		ADD A,_SL
		MOV _SL,A
		MOV A,_SH
		ADDC A,#0
		MOV _SH,A
		RET
DELAY:
		MOV R2,#2
LOOP_DELAY2:
		MOV R0,#255
LOOP_DELAY:
		MOV R1,#255
		DJNZ R1,$
		DJNZ R0,LOOP_DELAY
		DJNZ R2,LOOP_DELAY2
		RET
END