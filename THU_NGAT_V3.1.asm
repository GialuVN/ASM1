;KHAI BAO
HAFT_PO BIT P3.4   ;PHAT TIN HIEU KICH SCR (BAN KI DUONG)
HAFT_NE BIT P3.5	;PHAT TIN HIEU KICH SCR (BAN KI AM)
ADC_CT	BIT P3.7	;DIEU KHIEN ADC


_HIG DATA 30H
_LOW DATA 31H
T_HIG DATA 34H
T_LOW DATA 33H


VAL   DATA 35H
_PLU  DATA 32H

TEM DATA 20H
FLAG BIT TEM.0
SLEC BIT TEM.1
FLAG2 BIT TEM.2
;BAT DAU CHUONG TRINH C

ORG 0000H
JMP MAIN
ORG 0003H
JMP INT_EX0
ORG 000BH
JMP INT_TIMER0
ORG 0013H
JMP INT_EX1
MAIN:
	MOV IE,#87H		;CHO PHEP NGAT NGOAI 0,1 TIMER0
	MOV TMOD,#01H   ;CHE DO 1 16 BIT
	setb ip.1
	SETB IT1		;TAC DONG BANG CANH
	SETB IT0		;TAC DONG BANG CANH
	SETB ADC_CT
	SETB FLAG2
	MOV P1,#255
	MOV VAL,#255
	CALL ANGLE
	
	
	
;NHAN DU LIEU TU ADC	
REP_ADC:
	JNB FLAG2,OUT
	CLR FLAG2
	MOV A,P1
	CPL A
	MOV VAL,A
	CALL ANGLE
	CLR ADC_CT
OUT:
	JMP REP_ADC
	
;DONG BO BAN KI DUONG
INT_EX0:
		CLR TR0
		SETB HAFT_NE
		SETB HAFT_PO
		MOV _PLU,#10		;SO XUNG KICH
		MOV TH0,_HIG
		MOV TL0,_LOW
		SETB SLEC
		SETB TR0
		SETB FLAG2
		RETI
;DONG BO BAN KI AM		
INT_EX1:
		CLR TR0
		SETB HAFT_PO
		SETB HAFT_NE
		MOV _PLU,#10		;SO XUNG KICH
		MOV TH0,_HIG
		MOV TL0,_LOW
		CLR SLEC
		SETB TR0
		SETB ADC_CT
		SETB FLAG2
		RETI		
;XAC DINH VI TRI VA XUAT XUNG KICH
INT_TIMER0:
		MOV TH0,#HIGH(-50)
		MOV TL0,#LOW(-50)
		SETB TR0
		JNB SLEC,_EX0
		CPL HAFT_PO
		JMP END_EX1
_EX0:
		CPL HAFT_NE
END_EX1:
		DJNZ _PLU,EX_INT_TIMER0
		SETB HAFT_PO
		SETB HAFT_NE
		CLR TR0
EX_INT_TIMER0:
		RETI
		
;THAY DOI GOC KICH VAL (1 - 255)		
ANGLE:
		MOV T_LOW,#0
		MOV T_HIG,#0
		MOV R1,VAL
		CJNE R1,#0,RE_ADD_16
		INC R1
RE_ADD_16:
		CJNE R1,#255,RE2_ADD_16
		DEC R1
RE2_ADD_16:
		MOV A,#37
		CALL ADD_16
		DJNZ R1,RE2_ADD_16
		CALL COMPL
		MOV _HIG,T_HIG
		MOV _LOW,T_LOW
		RET	
;LAY BU SO 16 BIT
COMPL:
		MOV A,T_LOW
		CPL A
		MOV T_LOW,A
		MOV A,T_HIG
		CPL A
		MOV T_HIG,A
		MOV A,#1
		CALL ADD_16
		RET
;8BIT + 16BIT	
ADD_16:
		ADD A,T_LOW
		MOV T_LOW,A
		MOV A,T_HIG
		ADDC A,#0
		MOV T_HIG,A
		RET
END