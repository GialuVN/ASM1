_DS		BIT		P1.7
SH_CP	BIT		P1.6
ST_CP	BIT		P1.5



;RTC DS1307
SCL BIT P3.4
SDA	BIT P3.5
;SQW BIT P1.2

WIRTE	EQU		0D0H
READ	EQU		0D1H


_SEC	EQU		27H				;GIAY
_MIN	EQU		28H				;PHUT
_HOUR	EQU		29H				;GIO
_DAY	EQU		2AH				;THU
_DATE	EQU 	2BH				;NGAY
_MONT	EQU		2CH				;THANG
_YEAR	EQU		2DH				;NAM
_CONT	EQU		2EH				;


_TEMP	EQU		2FH				;TAM


CH_		BIT		_SEC.7			;BAT TAT DAO DONG 0--BAT,1--TAT
H12_24	BIT		_HOUR.6			;CHON CHE DO 12H OR 24H 0-- 24H, 1--12H
APM		BIT		_HOUR.5			; APM=1--PM, 0--AM


TEMP	DATA	30H
ADDR	DATA	31H
BYTES	DATA	32H
INDEX	DATA	33H
;------------------

R8		DATA	40H
R9		DATA	41H
R10		DATA	42H
R11		DATA	43H

C100MS	DATA	44H
C250MS	DATA	45H
C500MS 	DATA	46H
C1S		DATA	47H
C10S	DATA	48H



CALL DELAY50MS

;BIEN HIEN TH
TEP1	DATA	50H
TEP2	DATA	51H
YEA1	DATA	52H
YEA2	DATA	53H
MON1	DATA	54H
MON2	DATA	55H
DAT1	DATA	56H
DAT2	DATA	57H
DAY1	DATA	58H
DAY2	DATA	59H
SEC1	DATA	5AH
SEC2	DATA	5BH
MIN1	DATA	5CH
MIN2	DATA	5DH
HOR1	DATA	5EH
HOR2	DATA	5FH




;DINH NGHIA BIT
FLAGS	DATA	22H
F1S		BIT		FLAGS.7
FR		BIT		FLAGS.6
FY		BIT		FLAGS.5
FG		BIT		FLAGS.4

TEM1 	DATA 	20H

T50MS  BIT  TEM1.0
T250MS BIT  TEM1.1
T500MS BIT  TEM1.2
T1S	   BIT  TEM1.3
T10S   BIT  TEM1.4
T100MS BIT	TEM1.5
;--------------------------
ORG 	0000H
JMP		BEGIN
ORG		001BH
MOV TH1,#HIGH(-50000)
MOV TL1,#LOW (-50000)
CLR TF1
SETB T50MS
RETI


BEGIN:
MOV IE,#88H				;CHO PHEP NGAT DO BO DINH THOI 1
MOV TMOD,#10H			; TIMER 1 CHE DO 16BIT
SETB TF1
SETB TR1

MOV TEM1,#255


MOV C100MS,#2
MOV C250MS,#5
MOV C500MS,#2
MOV C1S,#2
MOV C10S,#10

	
SETB SCL
SETB SDA

CLR _DS
CLR SH_CP
CLR ST_CP
MOV FLAGS,#11110000B


MOV _TEMP,#36
CALL DE_TEMP

	MOV _SEC,#59H			;GIAY
	MOV _MIN,#41H
	MOV _HOUR,#13H
	MOV _DAY,#06H
	MOV _DATE,#17H
	MOV _MONT,#12H
	MOV _YEAR,#10H
	MOV _CONT,#00010000B

	CALL DELAY50MS
	MOV BYTES,#08H
	MOV ADDR,#00H
	MOV INDEX,#_SEC
	CALL MASTER_W
	





MAIN:
		CALL HIEN_THI
		CALL TIMING
		JMP MAIN
		
		
		
;----------GIA MA GIAY------------		
DE_SEC:
		MOV A,_SEC
		ANL A,#00001111B
		CALL DECODE_SEG
		MOV SEC1,A
		MOV A,_SEC
		SWAP A
		ANL A,#00000111B
		CALL DECODE_SEG
		MOV SEC2,A
		RET
;------------GIAI MA PHUT----------
DE_MIN:

		MOV A,_MIN
		ANL A,#00001111B
		CALL DECODE_SEG
		MOV MIN1,A
		MOV A,_MIN
		SWAP A
		ANL A,#00000111B
		CALL DECODE_SEG
		MOV MIN2,A
		RET
;--------GIAI MA GIO---------------
DE_HOR:
		MOV A,_HOUR
		ANL A,#00001111B
		CALL DECODE_SEG
		MOV HOR1,A
		MOV A,_HOUR
		SWAP A
		JNB H12_24,HR1
		ANL A,#00000001B
		JMP HR3
HR1:
		ANL A,#00000011B
HR3:
		CALL DECODE_SEG
		MOV HOR2,A
		RET
;----------GIAI MA THU-------------		
DE_DAY:
		MOV A,_DAY
		CALL DECODE_SEG
		MOV DAY1,A
		MOV DAY2,#3FH	;SO 0
		RET
;------------GIAI MA NGAY----------
DE_DATE:
		MOV A,_DATE
		ANL A,#00001111B
		CALL DECODE_SEG
		MOV DAT1,A
		MOV A,_DATE
		SWAP A
		ANL A,#00001111B
		CALL DECODE_SEG
		MOV DAT2,A
		RET
;----------GIAI MA THANG-----------
DE_MONT:
		MOV A,_MONT
		ANL A,#00001111B
		CALL DECODE_SEG
		MOV MON1,A
		MOV A,_MONT
		SWAP A
		ANL A,#00001111B
		CALL DECODE_SEG
		MOV MON2,A
		RET
;-----------GIAI MA NAM-------------
DE_YEAR:
		MOV A,_YEAR
		ANL A,#00001111B
		CALL DECODE_SEG
		MOV YEA1,A
		MOV A,_YEAR
		SWAP A
		ANL A,#00001111B
		CALL DECODE_SEG
		MOV YEA2,A
		RET
;-----------GIAI MA NHIET DO----------
DE_TEMP:
		MOV A,_TEMP
		MOV B,#10
		DIV AB
		CALL DECODE_SEG
		MOV TEP2,A
		MOV A,B
		CALL DECODE_SEG
		MOV TEP1,A		
		RET
;--------------GIAI MA FULL-------------
DE_ALL:
		CALL DE_SEC
		CALL DE_MIN
		CALL DE_HOR
		CALL DE_DAY
		CALL DE_DATE
		CALL DE_MONT
		CALL DE_YEAR
		RET
		
		
;===============================================		
;TANG MA BCD
INC_BCD:
		ADD A,#1
		DA A
		RET
		
;GIAI MA BCD - LED 7 DOAN
DECODE_SEG:
		MOV DPTR,#MALED
		MOVC A,@A+DPTR
		RET
		
;---------------------------------------------------		
;DOC DU LIEU TU DS1307 VOI CAC THAM SO: BYTES(SO BYTES DUOC DOC LIEN TIEP),INDEX(DIA CHI DICH 8051),ADDR(DIA CHI NGUON DS1307 )	
MASTER_R:
		PUSH 00H
		PUSH 07H
		CALL SELECT_ADDR
		CALL CON_S
		MOV TEMP,#READ
		CALL __BYTE_WIRTE	
		MOV R7,BYTES
		MOV R0,INDEX
__LOOP4:
		CALL ACK
		CALL __BYTE_READ
		MOV @R0,TEMP
		INC R0
		DJNZ R7,__LOOP4
		CALL N_ACK
		CALL CON_P
		POP 07H
		POP 00H
		RET
;GHI DU LIEU LEN DS1307 VOI CAC THAM SO: BYTES(SO BYTES DUOC GHI LIEN TIEP),INDEX(DIA CHI DU LIEU NGUON 8051),ADDR(DIA CHI DICH DS1307)
MASTER_W:
		PUSH 07H
		PUSH 00H
		CALL CON_S
		MOV TEMP,#WIRTE
		CALL __BYTE_WIRTE
		CALL ACK
		MOV TEMP,ADDR
		CALL __BYTE_WIRTE
		CALL ACK
		MOV R7,BYTES
		MOV R0,INDEX
LOOP3__:
		MOV A,@R0
		INC R0
		MOV TEMP,A
		CALL __BYTE_WIRTE
		CALL ACK
		DJNZ R7,LOOP3__
		CALL CON_P
		POP 00H
		POP 07H
		RET
;DINH CON TRO DU LIEU QUA ADDR		
SELECT_ADDR:
		CALL CON_S
		MOV TEMP,#WIRTE
		CALL __BYTE_WIRTE
		CALL ACK
		MOV TEMP,ADDR
		CALL __BYTE_WIRTE
		CALL ACK
		CALL CON_P
		RET
;PHAT 1BYTE DU LIEU TRONG TEMP

__BYTE_WIRTE:
		PUSH 07H
		MOV A,TEMP
		MOV R7,#8
LOOP1__:
		RLC A
		MOV SDA,C
		SETB SCL
		CALL DELAYP
		CLR SCL
		DJNZ R7,LOOP1__
		POP 07H
		RET
; NHAN 1 BYTE DU LIEU CAT VAO TEMP
__BYTE_READ:
		PUSH 07H
		MOV R7,#8
LOOP2__:	
		SETB SCL
		MOV C,SDA
		CALL DELAYP
		CLR SCL
		RLC A
		DJNZ R7,LOOP2__
		MOV TEMP,A
		POP 07H
		RET
; DIEU KIEN START
CON_S:
		SETB SDA
		SETB SCL
		CALL DELAYN
		CLR SDA
		CALL DELAYN
		CLR SCL
		RET
;DIEU KIEN STOP
CON_P:
		CLR SDA
		CLR SCL
		CALL DELAYN
		SETB SCL
		CALL DELAYN
		SETB SDA
		RET
;BAO DA NHAN/PHAT 1 BYTE
ACK:
		CLR SCL
		CLR SDA
		CALL DELAYP
		SETB SCL
		CALL DELAYP
		CLR SCL
		SETB SDA
		CALL DELAYP
		RET
;DAO CUA ACK
N_ACK:
		CLR SCL
		SETB SDA
		CALL DELAYP
		SETB SCL
		CALL DELAYP
		RET
		
;SHORT DELAY
DELAYP:
		NOP
		NOP
		RET
DELAYN:
		CALL DELAYP
		CALL DELAYP
		RET		
;--------------------------------------------		
;CHUONG TRINH HIEN THI (VIP)
HIEN_THI:
		MOV R0,#50H		;DIA CHI CUA SO CUOI CUNG
		MOV R8,#1
		MOV R9,#7		;SO DOAN CUA LED
LOOP_HT:
		MOV A,FLAGS
		MOV R10,#8		
LOOP_HTX:		
		MOV C,ACC.0
		MOV _DS,C
		SETB SH_CP
		CLR SH_CP
		RR A
		DJNZ R10,LOOP_HTX
		MOV R10,#16
LOOP_HT1:	
		MOV A,@R0
		MOV B,R8
		DIV AB
		MOV C,ACC.0
		MOV _DS,C
		SETB  SH_CP
		CLR   SH_CP
		INC R0
		DJNZ R10,LOOP_HT1
		MOV R0,#50H
		MOV R10,#8
		MOV A,R8
LOOP_HT2:
		MOV C,ACC.7
		MOV _DS,C
		SETB SH_CP
		CLR SH_CP
		RL A
		DJNZ R10,LOOP_HT2
		SETB ST_CP
		CLR  ST_CP
		JNB T1S,EX_T1S
		CLR T1S
		MOV BYTES,#7
		MOV ADDR,#00H
		MOV INDEX,#_SEC
		CALL MASTER_R
		CALL DE_ALL
EX_T1S:		
		MOV A,R8
		RL A
		MOV R8,A
		DJNZ R9,LOOP_HT
		RET	
;-------------------------------------	
;CAC TIMER
TIMING:
		JNB T50MS,EX_TIMING
		CLR T50MS
		DJNZ C100MS,EX_100MS
		CPL T100MS
		CPL FR
		MOV C100MS,#2
EX_100MS:
		DJNZ C250MS,EX_TIMING
		CPL T250MS
		CPL FG
		MOV C250MS,#5
		DJNZ C500MS,EX_TIMING
		CPL T500MS
		CPL FY
		MOV C500MS,#2
		DJNZ C1S,EX_TIMING
		CPL T1S
		CPL F1S
		MOV C1S,#2
		DJNZ C10S,EX_TIMING
		CPL T10S
		MOV C10S,#10
EX_TIMING:
		RET
;============CHUONG TRINH DELAY==========
DELAY2MS:
		MOV 7FH,#10
L_DELAY:
		MOV 7EH,#100
		DJNZ 7EH,$
		DJNZ 7FH,L_DELAY
		RET
;============CHUONG TRINH DELAY2==========
DELAY50MS:
		MOV 7FH,#100
L_DELAY50:
		MOV 7EH,#250
		DJNZ 7EH,$
		DJNZ 7FH,L_DELAY50
		RET



MALED:
	DB 03Fh,06h,05Bh,04Fh,066h,06Dh,07Dh,07h,07Fh,06Fh
;C
;N
;A
;B
;C
;D
;E
;F


END
