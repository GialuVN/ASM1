;==============define===============
OP1		BIT		P1.7
OP2		BIT		P1.6
OP3		BIT		P1.5
OP4		BIT		P1.4
OP5		BIT		P1.3
OP6		BIT		P1.2
OP7		BIT		P1.1
OP8		BIT		P1.0
OP9		BIT		P3.4
OP10	BIT		P3.5
OP11	BIT		P3.2
OP12	BIT		P3.3
OP13	BIT		P3.0
OP14	BIT		P3.1
STROTE  BIT		P3.7

;===bien dieu khien do RONG XUNG======
MT01 EQU 30H
MT02 EQU 31H
MT03 EQU 32H
MT04 EQU 33H
MT05 EQU 34H
MT06 EQU 35H
MT07 EQU 36H
MT08 EQU 37H
MT09 EQU 38H
MT10 EQU 39H
MT11 EQU 3AH
MT12 EQU 3BH
MT13 EQU 3CH
MT14 EQU 3DH

MT15 EQU 3EH
MT16 EQU 3FH
MT17 EQU 40H
MT18 EQU 41H
MT19 EQU 42H
MT20 EQU 43H
MT21 EQU 44H
MT22 EQU 45H

MT23 EQU 46H
MT24 EQU 47H
MT25 EQU 48H
MT26 EQU 49H
MT27 EQU 4AH
MT28 EQU 4BH

MT29 EQU 4CH
MT30 EQU 4DH
MT31 EQU 4EH
MT32 EQU 4FH

MT33 EQU 50H
MT34 EQU 51H
MT35 EQU 52H
MT36 EQU 53H
MT37 EQU 54H
MT38 EQU 55H
MT39 EQU 56H
MT40 EQU 57H

MT41 EQU 58H
MT42 EQU 59H
MT43 EQU 5AH
MT44 EQU 5BH
MT45 EQU 5CH
MT46 EQU 5DH

MT47 EQU 5EH
MT48 EQU 5FH





;==========BIEN DIEU KHIEN==============
RESOL  EQU 64		;DO PHAN GIAI
SPEED  EQU 60H
STEP   EQU 61H		;BUOC NHAY CON TRO
APOS   EQU 62H
BPOS   EQU 63H
SLE_MT EQU 64H
COUNT  EQU 65H
BACK   EQU 66H
TEM1   EQU 67H		
TEM2   EQU 68H		;BIEN TAM DA DUNG	
TEM3   EQU 69H



		
;============KHOI TAO BAN DAU===================
BEGIN:
	CALL DELAY
	MOV MT01,#0
	MOV MT02,#0
	MOV MT03,#0
	MOV MT04,#0
	MOV MT05,#0
	MOV MT06,#0
	MOV MT07,#0
	MOV MT08,#0
	MOV MT09,#0
	MOV MT10,#0
	MOV MT11,#0
	MOV MT12,#0
	MOV MT13,#0
	MOV MT14,#0
;==================CHUONG TRINH CHINH==============
MAIN:

CALL STYLE1

	
	JMP MAIN

STYLE1:
	CALL EFF9A
	CALL EFF9B
	
	CALL EFF10A
	CALL EFF10B
	CALL EFF10A
	CALL EFF10B
	CALL EFF5
	CALL EFF6
	CALL EFF_RIP1
	CALL EFF_RIP2
	CALL EFF_RIP3
	CALL EFF_RIP4
	CALL EFF_RIP5
	CALL EFF_RIP6
	
	CALL EFF7
	CALL EFF8
	
	CALL EFFX1
	CALL EFF1
	CALL EFF2
	CALL EFF3
	CALL EFF4
	CALL EFF_RIP4
	CALL EFF_RIP5
	CALL EFF_RIP6
	CALL EFF5
	CALL EFF6
	
	CALL EFF7
	CALL EFF8
	CALL EFF_RIP1
	CALL EFF_RIP2
	CALL EFF_RIP3
	CALL EFFX1
	CALL EFF5
	CALL EFF6
	RET
				
		

	
;======PWM CO CHINH TOC DO BIEN "SPEED"==================
PWM_SP:
		PUSH SPEED
R_PWM_SP:	
		CALL PWM_16C
		DJNZ SPEED,R_PWM_SP
		POP SPEED
		RET
;============CHUONG TRINH CON PHAT PWM===================
PWM_16C:
		PUSH 00H
		PUSH ACC
		MOV R0,#RESOL
		JMP CONT0
LOOP_PWM:
		DJNZ R0,CONT0
		MOV P1,#0
		MOV P3,#0
		SETB STROTE
		CLR STROTE
		JMP EX_PWM
CONT0:
		MOV A,R0	
		CJNE A,MT01,CONT1
		SETB OP1
CONT1:
		CJNE A,MT02,CONT2
		SETB OP2
CONT2:
		CJNE A,MT03,CONT3
		SETB OP3
CONT3:
		CJNE A,MT04,CONT4
		SETB OP4
CONT4:
		CJNE A,MT05,CONT5
		SETB OP5
CONT5:
		CJNE A,MT06,CONT6
		SETB OP6
CONT6:
		CJNE A,MT07,CONT7
		SETB OP7
CONT7:
		CJNE A,MT08,CONT8
		SETB OP8
CONT8:
		CJNE A,MT09,CONT9
		SETB OP9
CONT9:
		CJNE A,MT10,CONT10
		SETB OP10
CONT10:
		CJNE A,MT11,CONT11
		SETB OP11
CONT11:
		CJNE A,MT12,CONT12
		SETB OP12
CONT12:
		CJNE A,MT13,CONT13
		SETB OP13
CONT13:
		CJNE A,MT14,CONT14
		SETB OP14
CONT14:
		SETB STROTE
		CLR STROTE
		JMP LOOP_PWM
EX_PWM:
		POP ACC
		POP 00H
		RET
		
;=====HIEU UNG======================
EFFX1:
	MOV APOS,#MT01
	MOV BPOS,#MT14
	MOV SPEED,#20
	MOV BACK,#5
	CALL LOAD_F
	
	MOV R6,#13
	MOV R4,#13
	
	MOV DPTR,#T_INC1
	MOV SLE_MT,#MT01
	MOV APOS,#MT01
L2_EFFX1:	
	MOV COUNT,#8
	MOV SPEED,#30
	CALL LOAD_	
	MOV SPEED,#20
	MOV TEM3,R4
L_EFFX1:
	CALL R_AB
	CALL PWM_SP
	DJNZ TEM3,L_EFFX1
	DEC R4
	DEC BPOS
	DJNZ R6,L2_EFFX1
	MOV COUNT,#8
	MOV SPEED,#30
	CALL LOAD_	
	RET
;>TAT CA LED TAT
;>SANG TU TU TUNG LED
;>TU TRAI SANG PHAI
EFF1:
		PUSH TEM1
		MOV DPTR,#LOW_ALL
		MOV APOS,#MT01
		MOV BPOS,#MT14
		CALL LOAD_MT
		
		MOV SLE_MT,APOS
		MOV SPEED,#10
		MOV COUNT,#8
		MOV DPTR,#T_INC1
		MOV TEM1,BPOS
		INC TEM1
L_EFF1:		
		CALL LOAD_
		INC SLE_MT
		MOV A,SLE_MT
	
		CJNE A,TEM1,L_EFF1
		POP TEM1
		RET

;>TAT CA LED SANG
;>SANG TU TU TUNG LED
;>TU TRAI SANG PHAI
		
EFF2:
		PUSH TEM1
		MOV DPTR,#HIGH_ALL
		MOV APOS,#MT01
		MOV BPOS,#MT14
		CALL LOAD_MT
		
		MOV SLE_MT,APOS
		MOV SPEED,#10
		MOV COUNT,#8
		MOV DPTR,#T_DEC1
L_EFF2:		
		CALL LOAD_
		INC SLE_MT
		MOV A,SLE_MT
		MOV TEM1,BPOS
		INC TEM1
		CJNE A,TEM1,L_EFF2
		POP TEM1
		RET	

;>TAT CA LED TAT
;>LED SANG TUNG MUC
;>TU TRAI SANG PHAI	
		
EFF3:
		PUSH 04H
		PUSH TEM1
		MOV DPTR,#LOW_ALL
		MOV APOS,#MT01
		MOV BPOS,#MT14
		CALL LOAD_MT
	
		MOV SPEED,#20
		MOV COUNT,#1
		MOV DPTR,#T_INC1
		MOV TEM1,BPOS
		INC TEM1
		MOV R5,#8
L2_EFF3:
		MOV SLE_MT,APOS
L_EFF3:		
		CALL LOAD_
		INC SLE_MT
		MOV A,SLE_MT
		CJNE A,TEM1,L_EFF3
		INC DPTR
		DJNZ R5,L2_EFF3
		POP TEM1
		POP 04H
		RET	
;>TAT CA LED SANG
;>LED TAT TUNG MUC
;>TU TRAI SANG PHAI			
					
			
EFF4:
		PUSH 04H
		PUSH TEM1
		MOV DPTR,#HIGH_ALL
		MOV APOS,#MT01
		MOV BPOS,#MT14
		CALL LOAD_MT
		
		MOV SPEED,#20
		MOV COUNT,#1
		MOV DPTR,#T_DEC1
	
		MOV TEM1,BPOS
		INC TEM1
		MOV R4,#8
L2_EFF4:
		MOV SLE_MT,APOS
L_EFF4:		
		CALL LOAD_
		INC SLE_MT
		MOV A,SLE_MT
		
		CJNE A,TEM1,L_EFF4
		INC DPTR
		DJNZ R4,L2_EFF4
		POP TEM1
		POP 04H
		RET							
;>TAT CA LED TAT
;>SAO BANG TU TRAI SANG PHAI

EFF5:
		PUSH 04H
		MOV DPTR,#LOW_ALL
		MOV APOS,#MT01
		MOV BPOS,#MT22
		CALL LOAD_MT
		
		MOV DPTR,#T_INC1
		MOV APOS,#MT15
		MOV BPOS,#MT22
		CALL LOAD_MT
		
		MOV APOS,#MT01
		MOV BPOS,#MT22
		MOV SPEED,#30
		MOV R4,#14
L1_EFF5:		
		CALL R_AB
		CALL PWM_SP
		DJNZ R4,L1_EFF5
		
		MOV R4,#8
L2_EFF5:		
		CALL R_AB
		MOV R0,#MT14
		MOV @R0,#64
		CALL PWM_SP
		DJNZ R4,L2_EFF5
		POP 04H
		RET
;>TAT CA LED TAT
;>SAO BANG TU PHAI SANG TRAI
	
EFF6:
		PUSH 04H
		MOV DPTR,#LOW_ALL
		MOV APOS,#MT01
		MOV BPOS,#MT22
		CALL LOAD_MT
		
		MOV DPTR,#T_DEC1
		MOV APOS,#MT15
		MOV BPOS,#MT22
		CALL LOAD_MT
		
		MOV APOS,#MT01
		MOV BPOS,#MT22
		MOV SPEED,#30
		MOV R4,#14
L1_EFF6:		
		CALL R_BA
		CALL PWM_SP
		DJNZ R4,L1_EFF6
		
		MOV R4,#8
L2_EFF6:		
		CALL R_BA
		MOV R0,#MT01
		MOV @R0,#64
		CALL PWM_SP
		DJNZ R4,L2_EFF6
		POP 04H
		RET
;SANG DAN TAT CA LED
EFF7:
		MOV DPTR,#T_INC1
		MOV APOS,#MT01
		MOV BPOS,#MT14
		MOV COUNT,#8
		MOV SPEED,#40
		CALL LOAD_D
		RET
;TAT DAN TAT CA LED		
EFF8:
		MOV DPTR,#T_DEC1
		MOV APOS,#MT01
		MOV BPOS,#MT14
		MOV COUNT,#8
		MOV SPEED,#40
		CALL LOAD_D
		RET	
		
		
;==========	
EFF9A:
		MOV DPTR,#LOW_ALL
		MOV APOS,#MT01
		MOV BPOS,#MT14
		CALL LOAD_MT
		MOV SLE_MT,#MT01
		MOV R4,#14
L_EFF9A:		
		MOV DPTR,#T_INC1
		MOV COUNT,#8
		MOV SPEED,#5
		CALL LOAD_
		
		MOV DPTR,#T_DEC1
		MOV COUNT,#8
		CALL LOAD_
		INC SLE_MT
		DJNZ R4,L_EFF9A
		RET		
		
	EFF9B:
		MOV DPTR,#LOW_ALL
		MOV APOS,#MT01
		MOV BPOS,#MT14
		CALL LOAD_MT
		MOV SLE_MT,#MT14
		MOV R4,#14
L_EFF9B:		
		MOV DPTR,#T_INC1
		MOV COUNT,#8
		MOV SPEED,#5
		CALL LOAD_
		
		MOV DPTR,#T_DEC1
		MOV COUNT,#8
		CALL LOAD_
		DEC SLE_MT
		DJNZ R4,L_EFF9B
		RET			
		
		
;============		
		
EFF10A:
		MOV DPTR,#LOW_ALL
		MOV APOS,#MT01
		MOV BPOS,#MT14
		CALL LOAD_MT
		MOV DPTR,#T_INC1
		MOV APOS,#MT15
		MOV BPOS,#MT22
		CALL LOAD_MT
		MOV R4,#10
L1_EFF10A:		
		MOV DPTR,#T_INC1
		MOV COUNT,#8
		MOV SLE_MT,#MT01
		MOV SPEED,#5
		CALL LOAD_
		
		MOV DPTR,#T_DEC1
		CALL LOAD_
		DJNZ R4,L1_EFF10A
		
		
		MOV APOS,#MT01
		MOV BPOS,#MT22
		MOV SPEED,#20
		MOV R4,#22
L2_EFF10A:
		CALL R_AB
		CALL PWM_SP
		DJNZ R4,L2_EFF10A
		RET
;==============	
EFF10B:
		MOV DPTR,#LOW_ALL
		MOV APOS,#MT01
		MOV BPOS,#MT14
		CALL LOAD_MT
		MOV DPTR,#T_DEC1
		MOV APOS,#MT15
		MOV BPOS,#MT22
		CALL LOAD_MT
		
		MOV R4,#10
L1_EFF10B:		
		MOV DPTR,#T_INC1
		MOV COUNT,#8
		MOV SLE_MT,#MT14
		MOV SPEED,#5
		CALL LOAD_
		
		MOV DPTR,#T_DEC1
		CALL LOAD_
		DJNZ R4,L1_EFF10B
		
		
		MOV APOS,#MT01
		MOV BPOS,#MT22
		MOV SPEED,#20
		MOV R4,#22
L2_EFF10B:
		CALL R_BA
		CALL PWM_SP
		DJNZ R4,L2_EFF10B
		RET	
;===============	
EFF_RIP1:
		MOV DPTR,#RIPLE8
		MOV STEP,#100
		MOV SPEED,#30
		MOV APOS,#MT01
		MOV BPOS,#MT23
		CALL LOAD_MT
		CALL RIP_AB
		CALL RIP_BA
		RET

EFF_RIP2:
		MOV DPTR,#RIPLE1
		MOV STEP,#100
		MOV SPEED,#30
		MOV APOS,#MT01
		MOV BPOS,#MT14
		CALL LOAD_MT
		CALL RIP_AB
		CALL RIP_BA
		
		RET


EFF_RIP3:
		MOV DPTR,#RIPLE6
		MOV STEP,#100
		MOV SPEED,#30
		MOV APOS,#MT01
		MOV BPOS,#MT14
		CALL LOAD_MT
		CALL RIP_AB
		CALL RIP_BA
		RET

EFF_RIP4:
		MOV DPTR,#RIPLE7
		MOV STEP,#100
		MOV SPEED,#30
		MOV APOS,#MT01
		MOV BPOS,#MT14
		CALL LOAD_MT
		CALL RIP_AB
		CALL RIP_BA
		RET	
		
EFF_RIP5:
		MOV DPTR,#RIPLE2
		MOV STEP,#100
		MOV SPEED,#30
		MOV APOS,#MT01
		MOV BPOS,#MT14
		CALL LOAD_MT
		CALL RIP_AB
		CALL RIP_BA
		RET			
EFF_RIP6:
		MOV DPTR,#RIPLE3
		MOV STEP,#100
		MOV SPEED,#30
		MOV APOS,#MT01
		MOV BPOS,#MT14
		CALL LOAD_MT
		CALL RIP_AB
		CALL RIP_BA
		RET			
;==========DON SONG==========================
;DPTR
;STEP
;APOS
;BPOS
;SPEED
;CALL LOAD_MT

RIP_AB:
		PUSH STEP
L_RIP_AB:		
		CALL R_AB
		CALL PWM_SP
		DJNZ STEP,L_RIP_AB
		POP STEP
		RET
		
		
RIP_BA:
		PUSH STEP
L_RIP_BA:	
		CALL R_BA
		CALL PWM_SP
		DJNZ STEP,L_RIP_BA
			POP STEP
		RET	
;===========CHINH DO SANG TUNG LED==========
;DPTR
;COUNT
;SLE_MT
;SPEED
;CALL LOAD_

LOAD_:
		PUSH 00H
		PUSH DPH
		PUSH DPL
		PUSH COUNT
		
		MOV R0,SLE_MT
L_LOAD_:	
		MOV A,#0
		MOVC A,@A+DPTR
		MOV @R0,A
		INC DPTR
		CALL PWM_SP
		DJNZ COUNT,L_LOAD_
	
		POP COUNT
		POP DPL
		POP DPH
		POP 00H
		RET
		
;========CHINH DO SANG TAT CA LED THEO DAY DATA============
;DPTR
;COUNT
;APOS
;BPOS
;SPEED
;CALL LOAD_D	
LOAD_D:
		PUSH 00H
		PUSH 01H
		PUSH DPH
		PUSH DPL
		PUSH COUNT
		PUSH TEM1
		MOV TEM1,BPOS
		INC TEM1	
L_LOAD_D:	
		MOV R0,APOS
		MOV A,#0
		MOVC A,@A+DPTR
		MOV R1,A
L2_LOAD_D:
		MOV A,R1		
		MOV @R0,A
		MOV R1,A
		
		INC R0
		MOV A,R0
		CJNE A,TEM1,L2_LOAD_D
		CALL PWM_SP
		INC DPTR
		DJNZ COUNT,L_LOAD_D
		
		POP TEM1
		POP COUNT
		POP DPL
		POP DPH
		POP 01H
		POP 00H
		RET
		
		
;===========CHINH DO SANG TAT CA LED =============
;BACK
;APOS
;BPOS
;SPEED
;CALL LOAD_F
LOAD_F:
		PUSH 00H
		PUSH TEM1
		
		MOV TEM1,BPOS
		INC TEM1
		MOV R0,APOS
L_LOAD_F:		
		MOV A,BACK
		MOV @R0,A
		CALL PWM_SP
		INC R0
		MOV A,R0
		CJNE A,TEM1,L_LOAD_F	
			
		POP TEM1
		POP 00H			
		RET

;==========TAI DO RONG XUNG=======================
;DPTR
;APOS
;BPOS
;CALL LOAD_MT
;USING[ APOS = ADRESS1, BPOS = ADRESS2]
LOAD_MT:
		PUSH 00H
		PUSH TEM1
		MOV TEM1,BPOS
		INC TEM1
		MOV R0,APOS
RE_LOAD_MT:	
		MOV A,#0
		MOVC A,@A+DPTR
		MOV @R0,A
		INC R0
		INC DPTR
		MOV A,R0
		CJNE A,TEM1,RE_LOAD_MT
		
		POP TEM1
		POP 00H
		RET
;========QUAY TRAI===================
;APOS
;BPOS
;CALL R_BA
;USING[ APOS = ADRESS1, BPOS = ADRESS2]
R_BA:;(BPOS--->APOS)
		PUSH 00H
		PUSH 01H
		PUSH 02H
		PUSH TEM1
		CALL COMP_
		JC EX_LEFT
		MOV TEM1,APOS
		MOV R0,TEM1
		MOV A,@R0
		MOV R2,A
		MOV R0,TEM1
		INC TEM1
		MOV R1,TEM1
_LEFT:		
		MOV A,@R1
		MOV @R0,A
		INC R0
		INC R1
		MOV A,R0
		CJNE A,BPOS,_LEFT
		MOV R0,BPOS
		MOV A,R2
		MOV @R0,A
EX_LEFT:
		POP TEM1
		POP 02H
		POP 01H
		POP 00H
		RET		
;======QUAY PHAI================
;APOS
;BPOS
;CALL R_AB
;USING[ APOS = ADRESS1, BPOS = ADRESS2]
R_AB:;(APOS--->BPOS)
		PUSH 00H
		PUSH 01H
		PUSH 02H
		PUSH TEM1
		CALL COMP_
		JC EX_RIGHT
		MOV TEM1,BPOS
		MOV R0,TEM1
		MOV A,@R0
		MOV R2,A
		
		MOV R0,TEM1
		DEC TEM1
		MOV R1,TEM1
_RIGHT:		
		MOV A,@R1
		MOV @R0,A
		DEC R0
		DEC R1
		MOV A,R0
		CJNE A,APOS,_RIGHT
		
		MOV R0,APOS
		MOV A,R2
		MOV @R0,A
EX_RIGHT:		
		POP TEM1
		POP 02H
		POP 01H
		POP 00H
		RET
;==========APOS MUST >BPOS===========
COMP_:
		MOV A,APOS
		CJNE A,BPOS,NOT_EQ
		CLR C
		JMP KEEP
NOT_EQ:
		JC ACCEPT
		SETB C
		JMP KEEP
ACCEPT:
		CLR C
KEEP:
		RET
;====================================
;========GIAM DPTR [DPTR-STEP]======
DEC_DPTR:
		CLR C
		MOV A,DPL
		SUBB A,STEP
		MOV DPL,A
		MOV A,DPH
		SUBB A,#0
		MOV DPH,A
		RET
;=======TANG DPTR [DPTR+STEP]==========
INC_DPTR:
		MOV A,DPL
		ADD A,STEP
		MOV DPL,A
		MOV A,DPH
		ADDC A,#0
		MOV DPH,A
		RET
;==========================================
DELAY:
		PUSH 01H
		PUSH 00H
		MOV R0,#255
L_DELAY:
		MOV R1,#255
		DJNZ R1,$
		DJNZ R0,L_DELAY
		POP 00H
		POP 01H
		RET
;==========KHU VUC DATA====================
;
RIPLE1:
	DB 5,10,20,30,40,50,64,64,50,40,30,20,10,5
RIPLE2:
	DB 5,10,30,64,30,10,5,5,10,30,64,30,10,5
RIPLE3:
	DB 64,59,55,50,46,41,36,31,27,22,18,13,9,4
	
RIPLE4:
	DB 1,2,4,8,16,32,64,1,2,4,8,16,32,64
RIPLE5:
	DB 64,32,16,8,4,2,1,64,32,16,8,4,2,1	
	
RIPLE6:
	DB 1,2,4,8,16,32,64,64,32,16,8,4,2,1
RIPLE7:
	DB 64,32,16,8,8,16,32,16,8,4,8,16,32,64
RIPLE8:
	DB	1,2,4,8,16,16,8,4,2,1,2,4,8,16,32,64,64,32,16,8,4,2,1;(23)

;BUOC TANG/GIAM
T_INC1:
	DB 0,1,2,4,8,16,32,64
T_DEC1:
	DB 64,32,16,8,4,2,1,0
	
T_INC2:
	DB 0,4,8,12,16,18,22,26,30,34,38,42,46,50,54,56,60,64;(18)
T_DEC2:
	DB 64,60,56,54,50,46,42,38,34,30,26,22,18,16,12,8,4,0

;THIET LAP BAN DAU
LOW_ALL:
	DB 0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0
	
HIGH_ALL:
	DB 64,64,64,64,64,64,64,64,64,64,64,64,64,64,64,64,64,64,64,64,64,64,64,64,64,64,64
	DB 64,64,64,64,64,64,64,64,64,64,64,64,64,64,64,64,64,64,64,64,64,64,64,64,64,64,64
END






	
	
	
	
	
	
	