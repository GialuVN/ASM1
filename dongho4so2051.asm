; dinh nghia port
SEG DATA P1

LM1	BIT P3.3
LM2 BIT	P3.2
LH1 BIT	P3.1
LH2 BIT	P3.0

KEY1	BIT P3.4
KEY3	BIT P3.5
LAMP	BIT P3.7

;DINH NGIA BIEN BYTE
DL1 	DATA 50H
DL2		DATA 51H


HOR		DATA	37H
MIN		DATA	38H
SEC		DATA	39H

SEC1	DATA	40H
SEC2 	DATA	41H

MIN1	DATA	30H
MIN2	DATA	31H

HOR1	DATA	32H
HOR2	DATA	33H

C250MS	DATA	34H
C500MS 	DATA	35H
C1S		DATA	36H

;DINH NGHIA BIT
TEM1  DATA 20H
T50MS  BIT TEM1.0
T250MS BIT TEM1.1
T500MS BIT TEM1.2
T1S	  BIT TEM1.3

TEM2	DATA 21H
EN_D1	BIT TEM2.0		;EN =1
EN_D23	BIT TEM2.1

EN_L12	BIT TEM2.2
EN_L34	BIT TEM2.3

AM_PM	BIT TEM2.4			;1 AM/ 0 PM

;~~~~~~~~BEGIN~~~~~~~~~~~~~~~~~~~
ORG		0000H
JMP		BEGIN
ORG		0001BH
MOV TH1,#HIGH(-49991)
MOV TL1,#LOW (-49991)
CLR TF1
SETB T50MS
RETI


BEGIN:
MOV DPTR,#CODE_7
MOV IE,#88H		;CHO PHEP NGAT DO BO DINH THOI 1
MOV TMOD,#10H	; TIMER 1. CHE DO 16BIT
SETB TF1
SETB TR1

MOV C250MS,#5
MOV C500MS,#2
MOV C1S,#2

MOV HOR,#0
MOV MIN,#0
MOV SEC,#0
MOV SEC1,#0
MOV SEC2,#0
MOV MIN1,#0
MOV MIN2,#0
MOV HOR1,#0
MOV HOR2,#0


MAIN:
	CALL TIMING
	CALL CLOCK
	CALL DISP
	
	JMP MAIN
	
DISP:
	MOV A,MIN1
	MOVC A,@A+DPTR
	MOV SEG,A
	CLR LM1
	CALL DELAY1M
	SETB LM1
	
	MOV A,MIN2
	MOVC A,@A+DPTR
	MOV SEG,A
	CLR LM2
	CALL DELAY1M
	SETB LM2
	
	MOV A,HOR1
	MOVC A,@A+DPTR
	MOV SEG,A
	CLR LH1
	CALL DELAY1M
	SETB LH1
	
	MOV A,HOR2
	MOVC A,@A+DPTR
	MOV SEG,A
	CLR LH2
	CALL DELAY1M
	SETB LH2
	RET
	

	
	
	
	
	
	
	
CLOCK:
	JNB T1S,X_TIME
	CLR T1S
	INC SEC1
	MOV A,SEC1
	CJNE A,#10,X_TIME
	MOV SEC1,#0
	INC SEC2
	MOV A,SEC2
	CJNE A,#6,X_TIME
	MOV SEC2,#0
	INC MIN1
	MOV A,MIN1
	CJNE A,#10,X_TIME
	MOV MIN1,#0
	INC MIN2
	MOV A,MIN2
	CJNE A,#6,X_TIME
	MOV MIN2,#0
	INC HOR1
	MOV A,HOR1
	CJNE A,#10,EX_CLOCK
	MOV HOR1,#0
	INC HOR2
	MOV A,HOR2
	CJNE A,#3,X_TIME
EX_CLOCK:
	MOV A,HOR2
	CJNE A,#2,X_TIME
	MOV A,HOR1
	CJNE A,#4,X_TIME
	MOV HOR2,#0
	MOV HOR1,#0
	
	
X_TIME:
	
	RET
;CAC TIMER
TIMING:
		JNB T50MS,EX_TIMING
		CLR T50MS
		DJNZ C250MS,EX_TIMING
		SETB T250MS
		MOV C250MS,#5
		DJNZ C500MS,EX_TIMING
		SETB T500MS
		MOV C500MS,#2
		DJNZ C1S,EX_TIMING
		SETB T1S
		MOV C1S,#2
EX_TIMING:
		RET


DELAY1M:
		MOV DL1,#10
RE_DELAY1M:
		MOV DL2,#250
		DJNZ DL2,$
		DJNZ DL1,RE_DELAY1M
		RET
		



















CODE_7:
	DB	0C0H,0F9H,0A4H,0B0H,099H,092H,082H,0F8H,080H,090H
	
	
	END