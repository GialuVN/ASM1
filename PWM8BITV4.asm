;KHAI BAO
XLOW1 DATA 30H
XHIG1 DATA 31H
XLOW2 DATA 32H
XHIG2 DATA 33H
TEM DATA 20H
WAR1 BIT TEM.0
WAR2 BIT TEM.1
DIV2 BIT TEM.2
CHOSE BIT TEM.3


ORG 0
JMP BEGIN
;PHAT XUNG PWM 3.7KHZ KENH 1
ORG 000BH
	CPL P1.7
	CPL P1.4
	JNB WAR1,SECON1
	MOV TL0,XLOW1
	CLR WAR1
	RETI
;PHAT XUNG PWM 3.7KHZ KENH 2
ORG 001BH
	CPL P1.5
	CPL P1.2
	JNB WAR2,SECON2
	MOV TL1,XLOW2
	CLR WAR2
	RETI
SECON1:
	MOV TL0,XHIG1
	SETB WAR1
	RETI
SECON2:
	MOV TL1,XHIG2
	SETB WAR2
	RETI
BEGIN:
;KHOI TAO
MOV TMOD,#22H
MOV IE,#8AH
SETB TR0
SETB TF0
SETB TR1
SETB TR1
;CT CHINH
	MOV XLOW2,#127
	CALL CONTRO2
MAIN:
	MOV XLOW1,#255
	MOV R0,#5
LAP1:
	CALL CONTRO1
	JNB CHOSE,TIME1
	CALL DELAY
	JMP TIP1
TIME1:
	CALL DELAY2
TIP1:
	DJNZ R0,CONT_LAP1
	MOV R0,#5
	CALL STYLE2
CONT_LAP1:
	DEC XLOW1
	MOV A,XLOW1
	CJNE A,#0,LAP1
	MOV XLOW1,#5
LAP2:
	CALL CONTRO1
	JNB CHOSE,TIME2
	CALL DELAY
	JMP TIP2
TIME2:
	CALL DELAY2
TIP2:
	DJNZ R0,CONT_LAP2
	MOV R0,#1
	CALL STYLE2
CONT_LAP2:
	INC XLOW1
	MOV A,XLOW1
	CJNE A,#0,LAP2
	CPL P1.3
	JMP MAIN
	
STYLE2:
	JNB DIV2,TANG
	DEC XLOW2
	CALL CONTRO2
	MOV A,XLOW2
	CJNE A,#1,END_STYLE
	CLR DIV2
	CPL CHOSE
	CPL P1.6
	JMP END_STYLE
TANG:
	INC XLOW2
	CALL CONTRO2
	MOV A,XLOW2
	CJNE A,#255,END_STYLE
	SETB DIV2
	CPL P1.6
END_STYLE:
	RET
;DIEU CHINH DO RONG XUNG 1 - 255 KENH 1
CONTRO1:
	PUSH ACC
	MOV A,XLOW1
	CPL A
	ADD A,#1
	MOV XHIG1,A
	POP ACC
	RET
;DIEU CHINH DO RONG XUNG 1 - 255 KENH 2
CONTRO2:
	PUSH ACC
	MOV A,XLOW2
	CPL A
	ADD A,#1
	MOV XHIG2,A
	POP ACC
	RET
;DELAY
DELAY2:
	PUSH 00H
	PUSH 01H
	MOV R0,#50
LOOP_DELAY2:
	MOV R1,#100
	DJNZ R1,$
	DJNZ R0,LOOP_DELAY2
	POP 01H
	POP 00H
	RET
DELAY:
	PUSH 00H
	PUSH 01H
	MOV R0,#100
LOOP_DELAY:
	MOV R1,#100
	DJNZ R1,$
	DJNZ R0,LOOP_DELAY
	POP 01H
	POP 00H
	RET
END