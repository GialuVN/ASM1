;DINH NGHIA BIEN
COM_  DATA 30H
ADD_  DATA 31H
R8	  DATA 32H
R9    DATA 33H
R10   DATA 34H
R11   DATA 35H
R12   DATA 36H		;FREE
R13	  DATA 37H		;FREE
R14	  DATA 38H		;FREE
R15   DATA 39H		;FREE
TEM_  DATA 4AH

FLAG  DATA 20H
SLEC  BIT  FLAG.0
OKEY  BIT  FLAG.1
ORG 00
JMP MAIN
ORG 0013H
JMP INT_INT1
;KHOI TAO
MAIN:
		MOV IE,#84H
		SETB P3.3
		MOV COM_,#0
		MOV ADD_,#0
;BAT DAU CHUONG TRINH CHINH
SMAIN:
		MOV P0,COM_
		MOV P2,ADD_
		JMP SMAIN


;$$$$$$$$$$$$$--CHUONG TRINH NGAT GIAI MA REMOTE SONY--$$$$$$$$$$$$$$$$
INT_INT1:
		MOV TEM_,COM_	
		CLR OKEY
		MOV R9,#12
		MOV R8,#255
LAP4:
		JB P3.3,EX_INT1				;;KIEM TRA BIT START
		DJNZ R8,LAP4				;VA CHONG NHIEU(BIT START >>2.4mS)
		JNB P3.3,$	         		;CHO DONG BO
		JB P3.3,$					;CHUAN BI NHAN BIT DAU TIEN
		MOV ADD_,#0
		MOV COM_,#0
LAP3:
		CALL DELAY900X				;
		JNB P3.3,TIP				; XAC DINH BIT NHAN DC LA "0" HAY "1"
		CALL LGIC0	
		JMP CONT_TIP
TIP:
		CALL LGIC1
CONT_TIP:
		JNB OKEY,LAP3
EX_INT1:
		RETI
;==========NHAN BIT 0================
LGIC0:
		CLR SLEC
		CALL NAP
		JB P3.3,$
		RET
;========NHAN BIT 1==================
LGIC1:
		SETB SLEC
		CALL NAP
		JNB P3.3,$
		JB P3.3,$
		RET
;========NHAN DU LIEU================
NAP:
		JB OKEY,CON_NAP
		CALL DICH
		DJNZ R9,CON_NAP
		MOV R10,#4
COMPL:
		CLR SLEC
		CALL DICH
		DJNZ R10,COMPL
		SETB OKEY
		MOV A,COM_
		MOV C,ACC.7
		JC  CON_NAP			;KIEM TRA LOI
	;	MOV COM_,TEM_
CON_NAP:
		RET
;=============DICH DU LIEU===========
DICH:
		MOV A,ADD_
		MOV C,SLEC
		RRC A
		MOV ADD_,A
		MOV A,COM_
		RRC A
		MOV COM_,A
		RET
;=========CHUONG TRINH TRE 0.9mS=====
DELAY900X:
		MOV R11,#4
L_DELAY900X:		
		MOV R8,#100
		DJNZ R8,$
		DJNZ R11,L_DELAY900X
		RET
END