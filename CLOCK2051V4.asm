;=====DEFINITE=========
LED	EQU	P1		;XUAT RA LED
KY1	BIT	P3.4	;PHIM 1
KY2	BIT P3.5	;PHIM 2
LRM BIT P3.7	;DEN NGU 12H-6H SANG
;=====VARIANT BYTE=====
HOR DATA 31H	;12H
MIN DATA 32H	;<>
SEC DATA 33H	;<>
X_HOR DATA 34H	;24H
;=====VARIANT BIT======
BITS DATA 20H
APM BIT BITS.0	;AM/PM
INT BIT BITS.1	;BAO NGAT
H24	BIT	BITS.2	;12/24H
QUA BIT BITS.3	;1/4 GIAY
HAF BIT BITS.3	;1/2 GIAY
FUL BIT BITS.4	;1 GIAY
BHO BIT	BITS.5	;CHON GIO
BMI	BIT BITS.6	;CHON PHUT
KIP BIT BITS.7	;TANG CO GIU
SLEEP DATA 21H
SLE BIT SLEEP.0 ;DEN NGU
MIS BIT SLEEP.1 ;THOI GIAN HIEN GIAY/PHUT
EMN BIT SLEEP.2 ;BAT/TAT HIEN PHUT/GIAY
;+++++++++++++++++++++++++++++
ORG 0000H
JMP MAIN
ORG 000BH
JMP INT_0
MAIN:
;==========INITIAL===========
CALL DELAY
MOV HOR,#12		;12 GIO
MOV MIN,#0		;0 PHUT
MOV SEC,#0		;0 GIAY
MOV BITS,#0
SETB SLE		;BAT DEN NGU
SETB APM 		;BUOI SANG
SETB H24		;CHO DO 12H
MOV P3,#0FFH	;CHO P3 LEN MUC CAO
MOV TMOD,#01H	;BO DINH THOI 0 CHE DO 1
MOV IE,#82H		;CHO PHEP NGAT DO TIMER 0
MOV R0,#0
MOV R1,#0
MOV R2,#0
MOV R3,#0
MOV R4,#30
MOV R5,#10
MOV R6,#0
SETB TF0
BEGIN:	
		JNB KY1,H24_12
		JNB KY2,SET_TIME
		CALL GEN
		JMP BEGIN
;=======TONG HOP==========
GEN:
		CALL COUNT_SEC
		CALL CONTV_24H
		CALL DISP
		RET
;===CHON CHE DO 24H-12H===
H24_12:
		CPL H24
		MOV R3,#30
REP_H24_12:
		CALL GEN
		JB KY1,END_H24_12
		DJNZ R3,REP_H24_12
		CPL H24
		CPL SLE
		JNB SLE,CONT_SLE
		MOV R3,#100
		CLR LRM
REP_LRM:
		CALL COUNT_SEC
		CALL CONTV_24H
		CALL DELAY
		DJNZ R3,REP_LRM
		SETB LRM
		JMP END_H24_12
CONT_SLE:
		MOV R3,#30
		MOV R4,#20
REP_CONT_SLE:
		CALL GEN
		DJNZ R3,REP_CONT_SLE
		MOV R3,#30
		CPL LRM
		DJNZ R4,REP_CONT_SLE
		SETB LRM
END_H24_12:
		CALL GEN
		JNB KY1,$-2
		JMP BEGIN
;======TINH CHINH=========
;CHINH PHUT
SET_TIME:
		SETB EMN
		SETB MIS
		SETB BMI
		CLR BHO
		CALL GEN
		JNB KY2,$-2
RETURN:
		CALL GEN
		JNB KY1,INC_MIN
		JNB KY2,SET_HOR
		JMP RETURN
INC_MIN:
		INC MIN
		MOV A,MIN
		CJNE A,#60,REP_KIP
		MOV MIN,#0
;CHINH PHUT CO GIU
REP_KIP:
		CALL GEN
		DJNZ R5,REP_KIP
		MOV R5,#10
		CLR BMI
		JB KY1,CONT_REP
		JB KIP,INC_MIN
		DJNZ R4,CONT_REP
		MOV R4,#30
		SETB KIP
		JMP INC_MIN
CONT_REP:
		JNB KY1,REP_KIP
		MOV R4,#30
		MOV R5,#10
		CLR KIP
		SETB BMI
		CALL GEN
		JNB KY1,$-2
		JMP RETURN
;CHINH GIO
SET_HOR:
		SETB BHO
		CLR BMI
		CALL GEN
		JNB KY2,$-2
RETURN2:
		CALL GEN
		JNB KY1,INC_HOR
		JNB KY2,SET_LRM
		JMP RETURN2
INC_HOR:
		INC HOR
		MOV A,HOR
		CJNE A,#12,CONT_INC_HOR
		CPL APM
CONT_INC_HOR:
		CJNE A,#13,END_INC_HOR
		MOV HOR,#1		
END_INC_HOR:
		CALL GEN
		JNB KY1,END_INC_HOR
		JMP RETURN2
;TRO VE
SET_LRM:
		CLR EMN
		CLR BMI
		CLR BHO
		CPL LRM
		CALL GEN
		JNB KY2,$-2
		JMP BEGIN
;=======DEM GIAY==========
COUNT_SEC:
		JNB INT,END_FUL
		INC R0
		CJNE R0,#5,CONT_QUA
		MOV R0,#0
		CPL QUA
CONT_QUA:
		INC R1
		CJNE R1,#10,CONT_HAF
		MOV R1,#0
		CPL HAF				;PHAT XUNG NUA GIAY
CONT_HAF:
		INC R2
		CJNE R2,#20,END_FUL
		MOV R2,#0
		CPL FUL				;PHAT XUNG 1 GIAY
		CALL COUNT_TIME
		INC R6
		CJNE R6,#5,END_FUL
		MOV R6,#0
		JB EMN,END_FUL
		CPL MIS
END_FUL:
		CLR INT
		RET
;=====CHIA THOI GIAN=======
COUNT_TIME:
		INC SEC
		MOV A,SEC
		CJNE A,#60,END_C
		MOV SEC,#0
		INC MIN
		MOV A,MIN
		CJNE A,#60,END_C
		MOV MIN,#0
		INC HOR
		MOV A,HOR
		CJNE A,#12,CONT_C
		CPL APM
CONT_C:
		CJNE A,#13,END_C
		MOV HOR,#1
END_C:
		RET
;=====CHUYEN DOI 12H-24H======
CONTV_24H:
		JNB APM,CONT_CV
		MOV A,HOR
		CJNE A,#12,CONT1_CV
		MOV X_HOR,#24
		JMP END_CV
CONT1_CV:
		MOV X_HOR,HOR
		JMP END_CV
CONT_CV:
		MOV A,HOR
		CJNE A,#12,CONT3_CV
		MOV X_HOR,#12
		JMP END_CV
CONT3_CV:
		ADD A,#12
		MOV X_HOR,A
END_CV:
		RET
;==========HIEN THI==========
DISP:
;PHUT
		JNB BMI,END_BMI
		JB HAF,END_BMI
		CALL DELAY
		CALL DELAY
		JMP END1_HAF
END_BMI:
		JNB MIS,DISP_SEC		
		MOV A,MIN
		JMP CONT_DISP_SEC
DISP_SEC:
		MOV A,SEC
CONT_DISP_SEC:
		CALL H_TO_D
		PUSH ACC
		ANL A,#0FH
		CALL D_TO_7
		MOV P1,A
		JB H24,END_QUA
		JNB QUA,CONT2_QUA
		CPL P1.7
		JMP CONT2_QUA
END_QUA:
		JNB APM,CONT_NIG
		SETB P1.7
		JMP CONT2_QUA
CONT_NIG:
		CLR P1.7
CONT2_QUA:
		CLR P3.3
		CALL DELAY
		SETB P3.3
		POP ACC
		SWAP A
		ANL A,#0FH
		CALL D_TO_7
		MOV P1,A
		JNB FUL,$+4
		CLR P1.7
		CLR P3.2
		CALL DELAY
		SETB P3.2
END1_HAF:
;GIO	
		JNB BHO,END_BHO
		JB HAF,END_BHO
		CALL DELAY
		CALL DELAY
		JMP END2_HAF
END_BHO:
		JNB MIS,CONT_MINS
		JB H24,CONT_12
		MOV A,X_HOR
		JMP CONT_24
CONT_12:
		MOV A,HOR
CONT_24:
		JMP END_CONT_MINS
CONT_MINS:
		MOV A,MIN
END_CONT_MINS:	
		CALL H_TO_D
		PUSH ACC
		ANL A,#0FH
		CALL D_TO_7
		MOV P1,A
		JNB FUL,$+4
		CLR P1.7
		CLR P3.1
		CALL DELAY
		SETB P3.1
		POP ACC
		SWAP A
		ANL A,#0FH
		CALL D_TO_7
		MOV P1,A
		CLR P3.0
		CALL DELAY
		SETB P3.0
;DEN NGU 24H-6H
END2_HAF:
		JNB SLE,END_DISP
		MOV A,X_HOR
		CJNE A,#24,CONT_COM
		CLR LRM
CONT_COM:
		CJNE A,#6,END_DISP
		SETB LRM
END_DISP:
		RET
;======GIAI MA DEC SANG 7 DOAN======
D_TO_7:
		MOV DPTR,#CODE7
		MOVC A,@A+DPTR
		RET
;=====GIAI MA HEX SANG DEC==========
H_TO_D:
		MOV DPTR,#CODEX
		MOVC A,@A+DPTR
		RET
;===========TRI HOAN 1mS============
DELAY:
		PUSH 0
		PUSH 1
		MOV R0,#20
REP_DELAY:
		MOV R1,#100
		DJNZ R1,$
		DJNZ R0,REP_DELAY
		POP 1
		POP 0
		RET
;=========TAO XUNG CHUAN 50mS========
INT_0:
		MOV TL0,#LOW(-49995)
		MOV TH0,#HIGH(-49995)
		SETB TR0
		SETB INT
		RETI
CODE7:
	DB	0C0H,0F9H,0A4H,0B0H,099H,092H,082H,0F8H,080H,090H,0FFH
CODEX:
	DB	00H,01H,02H,03H,04H,05H,06H,07H,08H,09H,10H,11H,12H,13H,14H,15H,16H
	DB	17H,18H,19H,20H,21H,22H,23H,24H,25H,26H,27H,28H,29H,30H,31H,32H,33H,34H,35H,36H
	DB	37H,38H,39H,40H,41H,42H,43H,44H,45H,46H,47H,48H,49H,50H,51H,52H,53H,54H,55H,56H
	DB	57H,58H,59H,60H
END







