_DS		BIT		P1.7
SH_CP	BIT		P1.6
ST_CP	BIT		P1.5
KEY1	BIT		P1.4
KEY2	BIT		P1.3
KEY3	BIT		P1.2

_DQ		BIT		P3.7


_WAIT  EQU 25H	;BIEN DELAY

;============================
;RTC DS1307
SCL BIT P1.1
SDA	BIT P1.0
;SQW BIT P1.2
;---------------------------
WIRTE	EQU		0D0H
READ	EQU		0D1H
;---------------------------

_SEC	EQU		27H				;GIAY
_MIN	EQU		28H				;PHUT
_HOUR	EQU		29H				;GIO
_DAY	EQU		2AH				;THU
_DATE	EQU 	2BH				;NGAY
_MONT	EQU		2CH				;THANG
_YEAR	EQU		2DH				;NAM
_CONT	EQU		2EH				;


_TEMP	EQU		2FH				;TAM
;-----------------------
TEMP	EQU		30H
ADDR	EQU		31H
BYTES	EQU		32H
INDEX	EQU		33H
;================================

R8		EQU		40H
R9		EQU		41H
R10		EQU		42H
R11		EQU		43H
;----------------------------------
C100MS	EQU		44H
C250MS	EQU		45H
C500MS 	EQU		46H
C1S		EQU		47H
C10S	EQU		48H
;================================
;BIEN HIEN TH
TEP1	EQU		50H
TEP2	EQU		51H

YEA1	EQU		52H
YEA2	EQU		53H
MON1	EQU		54H
MON2	EQU		55H
DAT1	EQU		56H
DAT2	EQU		57H
DAY1	EQU		58H
DAY2	EQU		59H
SEC1	EQU		5AH
SEC2	EQU		5BH
MIN1	EQU		5CH
MIN2	EQU		5DH
HOR1	EQU		5EH
HOR2	EQU		5FH
;===================================
;BIEN TAM
T_SEC	EQU		60H
T_MIN	EQU		61H
T_HOR	EQU		62H
T_DAY	EQU		63H
T_DAT	EQU		64H
T_MON	EQU		65H
T_YEA	EQU		66H

BCD		DATA	67H
HEX		DATA	68H
;===================================
;DINH NGHIA BIT
FLAGS	DATA	22H
F1S		BIT		FLAGS.7
FR		BIT		FLAGS.6
FY		BIT		FLAGS.5
FG		BIT		FLAGS.4
NC1		BIT		FLAGS.3
NC2		BIT		FLAGS.2
NC3		BIT		FLAGS.1
NC4		BIT		FLAGS.0
;-----------------------------------
TEM1 	DATA 	20H

T50MS  BIT  	TEM1.0
T250MS BIT  	TEM1.1
T500MS BIT  	TEM1.2
T1S	   BIT  	TEM1.3
T10S   BIT  	TEM1.4
T100MS BIT		TEM1.5
_SAVE  BIT 		TEM1.6
;-----------------------------------
RTC		DATA	21H
SLE_T	BIT		RTC.5			;1---12H, 0---24H
T_APM	BIT		RTC.4
CH_		BIT		RTC.3			;BAT TAT DAO DONG 0--BAT,1--TAT
H12_24	BIT		RTC.2			;CHON CHE DO 12H OR 24H 0-- 24H, 1--12H
APM		BIT		RTC.1			; APM=1--PM, 0--AM
SETT	BIT		RTC.0			;CHON CHE DO CAI DAT/HIEN THI	CAI DAT---1, HT---0
;-----------------------------------
MASK 	DATA	23H

MAS1	BIT		MASK.0
MAS2	BIT		MASK.1
MAS3	BIT		MASK.2
MAS4	BIT		MASK.3	
MAS5	BIT		MASK.4
MAS6	BIT		MASK.5	
MAS7	BIT		MASK.6	
	
;-----------------------------------	
ORG 	0000H
JMP		BEGIN
ORG		001BH
MOV TH1,#HIGH(-50000)
MOV TL1,#LOW (-50000)
CLR TF1
SETB T50MS
RETI

;===================================================
BEGIN:
CALL DELAY50MS
MOV IE,#88H				;CHO PHEP NGAT DO BO DINH THOI 1
MOV TMOD,#10H			; TIMER 1 CHE DO 16BIT
SETB TF1
SETB TR1


MOV TEM1,#255

MOV C100MS,#2
MOV C250MS,#5
MOV C500MS,#2
MOV C1S,#2
MOV C10S,#10
MOV R5,#0
MOV _CONT,#10000000B
MOV FLAGS,#00000000B

MOV MASK,#00000000B

CLR SETT

SETB KEY1
SETB KEY2
SETB KEY3
	
SETB SCL
SETB SDA

CLR _DS
CLR SH_CP
CLR ST_CP

MOV _TEMP,#36
CALL DE_TEMP
SETB SLE_T
SETB _DQ	
;=======SETUP TH,TL AND CONFIG BIT==========
	CLR TR1
	CALL INIT_TIME
	MOV A,#0CCH
	CALL MASTER_WIRTE
	MOV A,#4EH
	CALL MASTER_WIRTE
	MOV A,#100			;TH
	CALL MASTER_WIRTE
	MOV A,#0			;TL
	CALL MASTER_WIRTE
	MOV A,#00011111B	;CONFIG
	CALL MASTER_WIRTE
	CALL INIT_TIME
	MOV A,#0CCH
	CALL MASTER_WIRTE
	MOV A,#48H
	CALL MASTER_WIRTE
	MOV R7,#25
LX_DELAY:
	CALL DELAY50MS		;COPY TO EEPROM
	DJNZ R7,LX_DELAY
	;==========RECALL E2==========================
	CALL INIT_TIME
	MOV A,#0CCH
	CALL MASTER_WIRTE
	MOV A,#0B8H
	CALL MASTER_WIRTE
	JNB _DQ,$
;=====================================================
	SETB TR1
;======================================================

;======================================================
MAIN:	
	CALL HIEN_THI
	CALL TIMING
	CALL ALL_KEY
	JNB T10S,MAIN
	MOV C10S,#5
	CLR T10S
	
	CALL INIT_TIME
	MOV A,#0CCH
	CALL MASTER_WIRTE
	MOV A,#44H
	CALL MASTER_WIRTE
L3_TMING:	
	CALL HIEN_THI
	CALL TIMING
	CALL ALL_KEY
	JNB _DQ,L3_TMING
	
	CALL INIT_TIME
	MOV A,#0CCH
	CALL MASTER_WIRTE
	MOV A,#0BEH
	CALL MASTER_WIRTE
	CALL MASTER_READ
	ANL A,#11110000B
	SWAP A
	MOV _TEMP,A
	CALL MASTER_READ
	ANL A,#00000111B
	SWAP A
	ORL A,_TEMP
	MOV _TEMP,A
	LCALL DE_TEMP
	JMP MAIN
	
	
	
BLINK:	
		INC R5
		MOV A,R5
		CJNE A,#7,COUNT_LED
		MOV R5,#0
COUNT_LED:
		MOV A,R5
		CJNE A,#0,CON1_COUNT_LED
		SETB FR
		CLR FY
		CLR FG
CON1_COUNT_LED:		
		CJNE A,#1,CON2_COUNT_LED
		CLR FR
		SETB FY
		CLR FG
CON2_COUNT_LED:		
		CJNE A,#2,CON3_COUNT_LED
		CLR FR
		CLR FY
		SETB FG
CON3_COUNT_LED:
		CJNE A,#3,CON4_COUNT_LED
		CLR FR
		CLR FY
		SETB FG
CON4_COUNT_LED:		
		CJNE A,#4,CON5_COUNT_LED
		CLR FR
		SETB FY
		CLR FG
CON5_COUNT_LED:		
		CJNE A,#5,CON6_COUNT_LED
		SETB FR
		CLR FY
		CLR FG
CON6_COUNT_LED:
		RET
;============KHOI TAO DS18B20=============
INIT_TIME:
		CLR _DQ
		MOV _WAIT,#250
		DJNZ _WAIT,$
		SETB _DQ
L1_INIT_TIME:
		MOV _WAIT,#30
		DJNZ _WAIT,$
		JNB _DQ,L1_INIT_TIME
		RET
;========================================
;=====MASTER WIRTE ONE BYTE==============
;EXAMPLE 
;MOV A,#DATA
;CALL MASTER_WIRTE
MASTER_WIRTE:
		PUSH 01H
		MOV R1,#8
REPEAT_MASTER_WB:		
		CLR _DQ
		MOV _WAIT,#7
		DJNZ _WAIT,$
		RRC A
		MOV _DQ,C	
		MOV _WAIT,#7
		DJNZ _WAIT,$
		SETB _DQ
		NOP
		DJNZ R1,REPEAT_MASTER_WB
		POP 01H
		RET	
;======================================
;============READ ONE BYTE=============
;EXAMPLE
;CALL MASTER WIRTE
;MOV [STORE],A
MASTER_READ:
		PUSH 01H
		MOV R1,#8
L_MASTER_RB:		
		CLR _DQ
		NOP
		NOP
		SETB _DQ
		MOV _WAIT,#5
		DJNZ _WAIT,$
		MOV C,_DQ
		RRC A
		MOV _WAIT,#50
		DJNZ _WAIT,$
		DJNZ R1,L_MASTER_RB
		POP 01H
		RET
;=======================================	
;=======================================	
;=============END DS18B20===============
		
		
		
ALL_KEY:
		JNB KEY1,CV1
		JNB KEY2,CV2
		JNB KEY3,CV3
		JMP EX_ALL_KEY
		
CV3:

		CALL HIEN_THI
		CALL TIMING
		JNB KEY3,CV3
		CPL SLE_T
		JMP EX_ALL_KEY		
CV2:
		
		CALL HIEN_THI
		CALL TIMING
		JNB KEY2,CV2	
		MOV A,_CONT
		CPL ACC.4
		MOV _CONT,A
		MOV BYTES,#01H
		MOV ADDR,#07H
		MOV INDEX,#_CONT
		CALL MASTER_W
		JMP EX_ALL_KEY
		
CV1:
		CALL HIEN_THI
		CALL TIMING
		JNB KEY1,CV1
	
		CALL CVH_DATA
		SETB SETT
		SETB MAS1
	L_CV1:
		CALL ALL_CONT
		JNB KEY1,CONT_MIN
		JNB KEY2,SEC_IN
		JNB KEY3,SEC_DE
		JMP L_CV1
SEC_IN:
		CALL INC_SEC
		
L_SEC_IN:
		CALL ALL_CONT		
		JNB KEY2,L_SEC_IN
		JMP L_CV1
SEC_DE:
		CALL DEC_SEC
		
L_SEC_DE:
		CALL ALL_CONT		
		JNB KEY3,L_SEC_DE
		JMP L_CV1		
CONT_MIN:
		CALL ALL_CONT
		JNB KEY1,CONT_MIN
		CLR MAS1
		SETB MAS2		
L_CONT_MIN:		
		CALL ALL_CONT
		JNB KEY1,CONT_HOR
		JNB KEY2,MIN_IN
		JNB KEY3,MIN_DE
		JMP	L_CONT_MIN
		

MIN_IN:
		CALL INC_MIN
		
L_MIN_IN:
		CALL ALL_CONT		
		JNB KEY2,L_MIN_IN
		JMP L_CONT_MIN
MIN_DE:
		CALL DEC_MIN
		
L_MIN_DE:
		CALL ALL_CONT		
		JNB KEY3,L_MIN_DE
		JMP L_CONT_MIN		



CONT_HOR:
		CALL ALL_CONT
		JNB KEY1,CONT_HOR
		CLR MAS2
		SETB MAS3		
L_CONT_HOR:		
		CALL ALL_CONT
		JNB KEY1,CONT_DAY
		JNB KEY2,HOR_IN
		JNB KEY3,HOR_DE
		JMP	L_CONT_HOR

HOR_IN:
		CALL INC_HOR
		
L_HOR_IN:
		CALL ALL_CONT		
		JNB KEY2,L_HOR_IN
		JMP L_CONT_HOR
HOR_DE:
		CALL DEC_HOR
		
L_HOR_DE:
		CALL ALL_CONT		
		JNB KEY3,L_HOR_DE
		JMP L_CONT_HOR		


CONT_DAY:
		CALL ALL_CONT
		JNB KEY1,CONT_DAY
		CLR MAS3
		SETB MAS4		
L_CONT_DAY:		
		CALL ALL_CONT
		JNB KEY1,CONT_DAT
		JNB KEY2,DAY_IN
		JNB KEY3,DAY_DE
		JMP	L_CONT_DAY


DAY_IN:
		CALL INC_DAY
		
L_DAY_IN:
		CALL ALL_CONT		
		JNB KEY2,L_DAY_IN
		JMP L_CONT_DAY
DAY_DE:
		CALL DEC_DAY
		
L_DAY_DE:
		CALL ALL_CONT		
		JNB KEY3,L_DAY_DE
		JMP L_CONT_DAY		


CONT_DAT:
		CALL ALL_CONT
		JNB KEY1,CONT_DAT
		CLR MAS4
		SETB MAS5		
L_CONT_DAT:		
		CALL ALL_CONT
		JNB KEY1,CONT_MON
		JNB KEY2,DAT_IN
		JNB KEY3,DAT_DE
		JMP	L_CONT_DAT


DAT_IN:
		CALL INC_DAT
		
L_DAT_IN:
		CALL ALL_CONT		
		JNB KEY2,L_DAT_IN
		JMP L_CONT_DAT
DAT_DE:
		CALL DEC_DAT
		
L_DAT_DE:
		CALL ALL_CONT		
		JNB KEY3,L_DAT_DE
		JMP L_CONT_DAT	


CONT_MON:
		CALL ALL_CONT
		JNB KEY1,CONT_MON
		CLR MAS5
		SETB MAS6		
L_CONT_MON:		
		CALL ALL_CONT
		JNB KEY1,CONT_YEA
		JNB KEY2,MON_IN
		JNB KEY3,MON_DE
		JMP	L_CONT_MON


MON_IN:
		CALL INC_MON
		
L_MON_IN:
		CALL ALL_CONT		
		JNB KEY2,L_MON_IN
		JMP L_CONT_MON
MON_DE:
		CALL DEC_MON
		
L_MON_DE:
		CALL ALL_CONT		
		JNB KEY3,L_MON_DE
		JMP L_CONT_MON	


CONT_YEA:
		CALL ALL_CONT
		JNB KEY1,CONT_YEA
		CLR MAS6
		SETB MAS7		
L_CONT_YEA:		
		CALL ALL_CONT
		JNB KEY1,CONT_FINISH
		JNB KEY2,YEA_IN
		JNB KEY3,YEA_DE
		JMP	L_CONT_YEA


YEA_IN:
		CALL INC_YEA
		
L_YEA_IN:
		CALL ALL_CONT		
		JNB KEY2,L_YEA_IN
		JMP L_CONT_YEA
YEA_DE:
		CALL DEC_YEA
		
L_YEA_DE:
		CALL ALL_CONT		
		JNB KEY3,L_YEA_DE
		JMP L_CONT_YEA	


CONT_FINISH:
		CALL ALL_CONT
		JNB KEY1,CONT_FINISH
		CALL FINISH
EX_ALL_KEY:
		RET
			

		
ALL_CONT:
		CALL TIMING
		CALL CVB_DATA
		CALL HIEN_THI
		RET
REFRESH:

		RET
;----------KET THUC DIEU CHINH----------
FINISH:
		MOV R1,#_SEC
		MOV R0,#T_SEC
L_FINISH:		
		MOV A,@R0
		CALL HEX2BCD
		SWAP A
		ORL A,B
		MOV @R1,A
		INC R1
		INC R0
		CJNE R1,#(_SEC+8),L_FINISH
		MOV BYTES,#07H
		MOV ADDR,#00H
		MOV INDEX,#_SEC
		CALL MASTER_W
		
		MOV A,_HOUR
		CLR ACC.6
		MOV _HOUR,A
		MOV BYTES,#01H
		MOV ADDR,#02H
		MOV INDEX,#_HOUR
		CALL MASTER_W
		MOV MASK,#0
		CLR SETT
		RET
;---------TANG GIAM GIAY----------
INC_SEC:
		INC T_SEC
		MOV A,T_SEC
		CJNE A,#60,EX_INC_SEC
		MOV T_SEC,#0
EX_INC_SEC:
		RET
		
DEC_SEC:
		DEC T_SEC
		MOV A,T_SEC
		CJNE A,#-1,EX_DEC_SEC
		MOV T_SEC,#59
EX_DEC_SEC:
		RET

;--------TANG_GIAM PHUT------------
INC_MIN:
		INC T_MIN
		MOV A,T_MIN
		CJNE A,#60,EX_INC_MIN
		MOV T_MIN,#0
EX_INC_MIN:
		RET
		
DEC_MIN:
		DEC T_MIN
		MOV A,T_MIN
		CJNE A,#-1,EX_DEC_MIN
		MOV T_MIN,#59
EX_DEC_MIN:
		RET

;--------TANG_GIAM GIO------------

INC_HOR:
		INC T_HOR
		MOV A,T_HOR
		CJNE A,#24,EX_INC_HOR
		MOV T_HOR,#0
EX_INC_HOR:
		MOV A,T_HOR
		CALL GET_APM_HEX
		RET
		
DEC_HOR:
		DEC T_HOR
		MOV A,T_HOR
		CJNE A,#-1,EX_DEC_HOR
		MOV T_HOR,#23
EX_DEC_HOR:
		MOV	A,T_HOR
		CALL GET_APM_HEX
		RET
		
;--------TANG_GIAM THU------------		
INC_DAY:
		INC T_DAY
		MOV A,T_DAY
		CJNE A,#8,EX_INC_DAY
		MOV T_DAY,#1
EX_INC_DAY:
		RET
		
DEC_DAY:
		DEC T_DAY
		MOV A,T_DAY
		CJNE A,#0,EX_DEC_DAY
		MOV T_DAY,#7
EX_DEC_DAY:
		RET
		
;--------TANG_GIAM NGAY------------		

INC_DAT:
		INC T_DAT
		MOV A,T_DAT
		CJNE A,#32,EX_INC_DAT
		MOV T_DAT,#1
EX_INC_DAT:
		RET
		
DEC_DAT:
		DEC T_DAT
		MOV A,T_DAT
		CJNE A,#0,EX_DEC_DAT
		MOV T_DAT,#31
EX_DEC_DAT:
		RET		
;--------TANG_GIAM THANG------------		
INC_MON:
		INC T_MON
		MOV A,T_MON
		CJNE A,#13,EX_INC_MON
		MOV T_MON,#1
EX_INC_MON:
		RET
		
DEC_MON:
		DEC T_MON
		MOV A,T_MON
		CJNE A,#0,EX_DEC_MON
		MOV T_MON,#12
EX_DEC_MON:
		RET		
;--------TANG_GIAM NAM------------		
INC_YEA:
		INC T_YEA
		MOV A,T_YEA
		CJNE A,#100,EX_INC_YEA
		MOV T_YEA,#0
EX_INC_YEA:
		RET
		
DEC_YEA:
		DEC T_YEA
		MOV A,T_YEA
		CJNE A,#-1,EX_DEC_YEA
		MOV T_YEA,#99
EX_DEC_YEA:
		RET					
;----------HEX TO BCD-------------
;chuyen doi gia tri so hex sang so BCD
CVB_DATA:
		MOV A,T_SEC
		CALL HEX2BCD
		JNB MAS1,CORSS1
		JNB T500MS,EX_MAS1
CORSS1:		
		MOV SEC2,A
		MOV SEC1,B
		JMP EX_MAS11
EX_MAS1:
		MOV SEC2,#10
		MOV SEC1,#10
EX_MAS11:
		MOV A,T_MIN
		CALL HEX2BCD
		JNB MAS2,CORSS2
		JNB T500MS,EX_MAS2
CORSS2:		
		MOV MIN2,A
		MOV MIN1,B
		JMP EX_MAS22
EX_MAS2:		
		MOV MIN2,#10
		MOV MIN1,#10
EX_MAS22:
		MOV A,T_HOR
		CALL HEX2BCD
		JNB MAS3,CORSS3
		JNB T500MS,EX_MAS3
CORSS3:		
		MOV HOR2,A
		MOV HOR1,B	
		JMP EX_MAS33
EX_MAS3:		
		MOV HOR2,#10
		MOV HOR1,#10
EX_MAS33:		
		MOV A,T_DAY
		CALL HEX2BCD
		JNB MAS4,CORSS4
		JNB T500MS,EX_MAS4
CORSS4:	
		INC B			
		MOV DAY2,A
		MOV DAY1,B
		MOV A,B
		CJNE A,#8,EX_CORSS4
		MOV DAY1,#12
		MOV DAY2,#11
EX_CORSS4:		
		JMP EX_MAS44
EX_MAS4:		
		MOV DAY2,#10
		MOV DAY1,#10
EX_MAS44:		
		MOV A,T_DAT
		CALL HEX2BCD
		JNB MAS5,CORSS5
		JNB T500MS,EX_MAS5
CORSS5:				
		MOV DAT2,A
		MOV DAT1,B
		JMP EX_MAS55
EX_MAS5:		
		MOV DAT2,#10
		MOV DAT1,#10
EX_MAS55:		
		MOV A,T_MON
		CALL HEX2BCD
		JNB MAS6,CORSS6
		JNB T500MS,EX_MAS6
CORSS6:				
		MOV MON2,A
		MOV MON1,B
		JMP EX_MAS66
EX_MAS6:		
		MOV MON2,#10
		MOV MON1,#10
EX_MAS66:		
		MOV A,T_YEA
		CALL HEX2BCD

		JNB MAS7,CORSS7
		JNB T500MS,EX_MAS7
CORSS7:				
		MOV YEA2,A
		MOV YEA1,B
		JMP EX_MAS77
EX_MAS7:		
		MOV YEA2,#10
		MOV YEA1,#10
EX_MAS77:		
		MOV R0,#52H
L_CVB_DATA:		
		MOV A,@R0
		CALL DECODE_SEG
		MOV @R0,A
		INC R0
		CJNE R0,#60H,L_CVB_DATA
		RET
;----------BCD TO HEX--------------
CVH_DATA:
		MOV A,_SEC
		CLR ACC.7
		MOV _SEC,A
		MOV A,_HOUR
		JNB H12_24,CONT1_ADJ_DATA
		ANL A,#00011111B
		JMP CONT2_ADJ_DATA
CONT1_ADJ_DATA:			
		ANL A,#00111111B	
CONT2_ADJ_DATA:
		MOV _HOUR,A
		MOV R0,#_SEC
		MOV R1,#T_SEC
L_CVH_DATA:
		MOV BCD,@R0
		CALL BCD2HEX
		MOV @R1,HEX
		INC R0
		INC R1
		CJNE R0,#(_SEC+8),L_CVH_DATA
		RET
;-----------FUNCTION HEX TO BCD-------------
HEX2BCD:
		MOV B,#10
		DIV AB
			RET
;----------FUNCTION BCD TO HEX--------------			
BCD2HEX:
		MOV A,BCD
		SWAP A
		ANL A,#00001111B
		CJNE A,#0,CONT_BCD2HEX
		MOV HEX,BCD
		JMP EX_BCD2HEX
CONT_BCD2HEX:
		MOV A,BCD
CONT3_BCD2HEX:		
		CLR C
		SUBB A,#6
		MOV HEX,A
		MOV B,#10
		DIV AB
		SWAP A
		ORL A,B
		CJNE A,BCD,CONT2_BCD2HEX
		JMP EX_BCD2HEX		
CONT2_BCD2HEX:
		MOV A,HEX
		JMP CONT3_BCD2HEX		
EX_BCD2HEX:
		RET
;----------GIAI MA GIAY------------		
DE_SEC:
		MOV A,_SEC
		ANL A,#00001111B
		CALL DECODE_SEG
		MOV SEC1,A
		MOV A,_SEC
		SWAP A
		ANL A,#00000111B
		CALL DECODE_SEG
		MOV SEC2,A
		RET
;------------GIAI MA PHUT----------
DE_MIN:

		MOV A,_MIN
		ANL A,#00001111B
		CALL DECODE_SEG
		MOV MIN1,A
		MOV A,_MIN
		SWAP A
		ANL A,#00000111B
		CALL DECODE_SEG
		MOV MIN2,A
		RET
;--------GIAI MA GIO---------------
DE_HOR:
		MOV A,_HOUR
		ANL A,#00001111B
		CALL DECODE_SEG
		MOV HOR1,A
		MOV A,_HOUR
		SWAP A
		JNB H12_24,HR1
		ANL A,#00000001B
		JMP HR3
HR1:
		ANL A,#00000011B
HR3:
		CALL DECODE_SEG
		MOV HOR2,A
		RET
;----------GIAI MA THU-------------		
DE_DAY:
		MOV A,_DAY
		INC A
		CJNE A,#8,CONT_DE_DAY
		MOV A,#12
		CALL DECODE_SEG
		MOV DAY1,A
		MOV A,#11
		CALL DECODE_SEG
		MOV DAY2,A
		JMP EX_DE_DAY
CONT_DE_DAY:	
		CALL DECODE_SEG
		MOV DAY1,A
		MOV DAY2,#3FH	;SO 0
EX_DE_DAY:		
		RET
;------------GIAI MA NGAY----------
DE_DATE:
		MOV A,_DATE
		ANL A,#00001111B
		CALL DECODE_SEG
		MOV DAT1,A
		MOV A,_DATE
		SWAP A
		ANL A,#00001111B
		CALL DECODE_SEG
		MOV DAT2,A
		RET
;----------GIAI MA THANG-----------
DE_MONT:
		MOV A,_MONT
		ANL A,#00001111B
		CALL DECODE_SEG
		MOV MON1,A
		MOV A,_MONT
		SWAP A
		ANL A,#00001111B
		CALL DECODE_SEG
		MOV MON2,A
		RET
;-----------GIAI MA NAM-------------
DE_YEAR:
		MOV A,_YEAR
		ANL A,#00001111B
		CALL DECODE_SEG
		MOV YEA1,A
		MOV A,_YEAR
		SWAP A
		ANL A,#00001111B
		CALL DECODE_SEG
		MOV YEA2,A
		RET
;-----------GIAI MA NHIET DO----------
DE_TEMP:
		MOV A,_TEMP
		MOV B,#10
		DIV AB
		CALL DECODE_SEG
		MOV TEP2,A
		MOV A,B
		CALL DECODE_SEG
		MOV TEP1,A		
		RET
;--------------GIAI MA FULL-------------
DE_ALL:
		MOV A,_HOUR
		CALL GET_APM_BCD
		CALL GIVE_FLAG
		CALL DE_SEC
		CALL DE_MIN
		CALL DE_HOR
		CALL DE_DAY
		CALL DE_DATE
		CALL DE_MONT
		CALL DE_YEAR
		RET
;------------LAY GIO 24/12 AND APM-------		

GIVE_FLAG:
		MOV A,_HOUR
		MOV C,ACC.6
		MOV H12_24,C
		JNB H12_24,CONT_GIVE_FLAG
		MOV C,ACC.5
		MOV APM,C
		JMP EX_GIVE_FLAG
CONT_GIVE_FLAG:		
		CLR APM
EX_GIVE_FLAG:
		RET
;--------------APM-------------------------
GET_APM_BCD:
		CJNE A,#00H,CONT1_GET_APM1
		SETB T_APM
		JMP EX_GET_APM1
CONT1_GET_APM1:		
		CJNE A,#12H,EX_GET_APM1
		CLR T_APM
EX_GET_APM1:
		RET
		
		
GET_APM_HEX:
		CJNE A,#0,CONT11_GET_APM2
		SETB T_APM
		JMP EX_GET_APM2
CONT11_GET_APM2:
		CJNE A,#23,CONT1_GET_APM2
		CLR T_APM
		JMP EX_GET_APM2
CONT1_GET_APM2:		
		CJNE A,#12,EX1_GET_APM2
		CLR T_APM
		JMP EX_GET_APM2
EX1_GET_APM2:
		CJNE A,#11,EX_GET_APM2
		SETB T_APM
EX_GET_APM2:
		RET			
;------------CHINH H24-TO-12---------------
H24TO12:
		JNB SLE_T,EX_H24TO12
		JNB T_APM,CONT_H24TO12
		CJNE A,#00H,EX_H24TO12
		MOV A,#12H
		JMP EX_H24TO12
CONT_H24TO12:	
		CJNE A,#12H,CONT2_H24TO12
		MOV A,#12H
		JMP EX_H24TO12
CONT2_H24TO12:		
		MOV BCD,A
		CALL BCD2HEX
		MOV A,HEX
		CLR C
		SUBB A,#12
		CALL HEX2BCD
		SWAP A
		ORL A,B
EX_H24TO12:	
		RET
;===============================================		

		
;GIAI MA BCD - LED 7 DOAN
DECODE_SEG:
		MOV DPTR,#MALED
		MOVC A,@A+DPTR
		RET
		
;---------------------------------------------------		
;DOC DU LIEU TU DS1307 VOI CAC THAM SO: BYTES(SO BYTES DUOC DOC LIEN TIEP),INDEX(DIA CHI DICH 8051),ADDR(DIA CHI NGUON DS1307 )	
MASTER_R:
		PUSH 00H
		PUSH 07H
		CALL SELECT_ADDR
		CALL CON_S
		MOV TEMP,#READ
		CALL __BYTE_WIRTE	
		MOV R7,BYTES
		MOV R0,INDEX
__LOOP4:
		CALL ACK
		CALL __BYTE_READ
		MOV @R0,TEMP
		INC R0
		DJNZ R7,__LOOP4
		CALL N_ACK
		CALL CON_P
		POP 07H
		POP 00H
		RET
;GHI DU LIEU LEN DS1307 VOI CAC THAM SO: BYTES(SO BYTES DUOC GHI LIEN TIEP),INDEX(DIA CHI DU LIEU NGUON 8051),ADDR(DIA CHI DICH DS1307)
MASTER_W:
		PUSH 07H
		PUSH 00H
		CALL CON_S
		MOV TEMP,#WIRTE
		CALL __BYTE_WIRTE
		CALL ACK
		MOV TEMP,ADDR
		CALL __BYTE_WIRTE
		CALL ACK
		MOV R7,BYTES
		MOV R0,INDEX
LOOP3__:
		MOV A,@R0
		INC R0
		MOV TEMP,A
		CALL __BYTE_WIRTE
		CALL ACK
		DJNZ R7,LOOP3__
		CALL CON_P
		POP 00H
		POP 07H
		RET
;DINH CON TRO DU LIEU QUA ADDR		
SELECT_ADDR:
		CALL CON_S
		MOV TEMP,#WIRTE
		CALL __BYTE_WIRTE
		CALL ACK
		MOV TEMP,ADDR
		CALL __BYTE_WIRTE
		CALL ACK
		CALL CON_P
		RET
;PHAT 1BYTE DU LIEU TRONG TEMP

__BYTE_WIRTE:
		PUSH 07H
		MOV A,TEMP
		MOV R7,#8
LOOP1__:
		RLC A
		MOV SDA,C
		SETB SCL
		CALL DELAYP
		CLR SCL
		DJNZ R7,LOOP1__
		POP 07H
		RET
; NHAN 1 BYTE DU LIEU CAT VAO TEMP
__BYTE_READ:
		PUSH 07H
		MOV R7,#8
LOOP2__:	
		SETB SCL
		MOV C,SDA
		CALL DELAYP
		CLR SCL
		RLC A
		DJNZ R7,LOOP2__
		MOV TEMP,A
		POP 07H
		RET
; DIEU KIEN START
CON_S:
		SETB SDA
		SETB SCL
		CALL DELAYN
		CLR SDA
		CALL DELAYN
		CLR SCL
		RET
;DIEU KIEN STOP
CON_P:
		CLR SDA
		CLR SCL
		CALL DELAYN
		SETB SCL
		CALL DELAYN
		SETB SDA
		RET
;BAO DA NHAN/PHAT 1 BYTE
ACK:
		CLR SCL
		CLR SDA
		CALL DELAYP
		SETB SCL
		CALL DELAYP
		CLR SCL
		SETB SDA
		CALL DELAYP
		RET
;DAO CUA ACK
N_ACK:
		CLR SCL
		SETB SDA
		CALL DELAYP
		SETB SCL
		CALL DELAYP
		RET
		
;SHORT DELAY
DELAYP:
		NOP
		NOP
		RET
DELAYN:
		CALL DELAYP
		CALL DELAYP
		RET		
;--------------------------------------------		
;CHUONG TRINH HIEN THI (VIP)
HIEN_THI:
		MOV R0,#50H		;DIA CHI CUA SO CUOI CUNG
		MOV R8,#1
		MOV R9,#7		;SO DOAN CUA LED
LOOP_HT:
		MOV A,FLAGS
		MOV R10,#8		
LOOP_HTX:		
		MOV C,ACC.0
		MOV _DS,C
		SETB SH_CP
		CLR SH_CP
		RR A
		DJNZ R10,LOOP_HTX
		MOV R10,#16
LOOP_HT1:	
		MOV A,@R0
		MOV B,R8
		DIV AB
		MOV C,ACC.0
		MOV _DS,C
		SETB  SH_CP
		CLR   SH_CP
		INC R0
		DJNZ R10,LOOP_HT1
		MOV R0,#50H
		MOV R10,#8
		MOV A,R8
LOOP_HT2:
		MOV C,ACC.7
		MOV _DS,C
		SETB SH_CP
		CLR SH_CP
		RL A
		DJNZ R10,LOOP_HT2
		SETB ST_CP
		CLR  ST_CP
		JB SETT,EX_T1S
		JNB T1S,EX_T1S
		CLR T1S
		MOV BYTES,#7
		MOV ADDR,#00H
		MOV INDEX,#_SEC
		CALL MASTER_R
		CALL DE_ALL
		JMP EX_T1S2
EX_T1S:	
		CALL DELAY2MS
EX_T1S2:
		MOV A,R8
		RL A
		MOV R8,A
		DJNZ R9,LOOP_HT
		RET	
;-------------------------------------	
;CAC TIMER
TIMING:
		JNB T50MS,EX_TIMING
		CLR T50MS
		DJNZ C100MS,EX_100MS
		CPL T100MS
		MOV C100MS,#2
EX_100MS:
		DJNZ C250MS,EX_TIMING
		CPL T250MS
		CALL BLINK
		MOV C250MS,#5
		DJNZ C500MS,EX_TIMING
		CPL T500MS
		CPL F1S
		MOV C500MS,#2
		DJNZ C1S,EX_TIMING
		CPL T1S
		MOV C1S,#2
		DJNZ C10S,EX_TIMING
		CPL T10S
		MOV C10S,#10
EX_TIMING:
		RET
;============CHUONG TRINH DELAY==========
DELAY2MS:
		MOV 7FH,#12
L_DELAY:
		MOV 7EH,#100
		DJNZ 7EH,$
		DJNZ 7FH,L_DELAY
		RET
;============CHUONG TRINH DELAY2==========
DELAY50MS:
		MOV 7FH,#100
L_DELAY50:
		MOV 7EH,#250
		DJNZ 7EH,$
		DJNZ 7FH,L_DELAY50
		RET



MALED:
	DB 03Fh,06h,05Bh,04Fh,066h,06Dh,07Dh,07h,07Fh,06Fh,00H,00111001B,01010100B
;C
;N
;A
;B
;C
;D
;E
;F


END
