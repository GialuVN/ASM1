;KHAI BAO BIEN
TEM DATA 20H	;LUU GIA TRI DEM
ORG 00H
;KHOI TAO
	MOV DPTR,#MA_LED	;NAP VI TRI MA_LED CHO CON TRO
	MOV TEM,#0	;
	MOV TMOD,#01H	; TIMER 0 CHE DO 1(16 BIT)
;CHUONG TRINH CHINH	
MAIN:
	MOV R0,#10
LAP1:
	MOV TL0,LOW(-50000)
	MOV TH0,HIGH(-50000)
	SETB TR0	;BAT DAU TINH GIO
LAP2:
	CALL HIEN_THI	;HIEN THI LIEN TUC RA LED
	JNB TF0,LAP2
	CLR TF0
	CLR TR0
	DJNZ R0,LAP1
	CALL TANG
	JMP MAIN
;CHUONG TRINH CON TANG GIA TRI DEM
TANG:
	MOV A,TEM
	ADD A,#1 ;CONG A VOI 1
	DA A	;HIEU CHINH THAP PHAN
	MOV TEM,A
	CJNE A,#60H,EXIT
	MOV TEM,#0
EXIT:
	RET
;CHUONG TRINH CON HIEN THI
HIEN_THI:
	MOV A,TEM
	ANL A,#0FH	; XOA 4 BIT CAO
	MOVC A,@A+DPTR	; GIAI MA
	MOV P0,A	;HIEN THI HANG DON VI
	CLR P2.1	; CHON LED HANG DON VI
	SETB P2.0
	CALL DELAY5MS
	MOV P0,#0FFH
	MOV A,TEM
	SWAP A		;QUAY THANH GHI A
	ANL A,#0FH	;XOA 4 BIT CAO
	MOVC A,@A+DPTR	;GIAI MA
	MOV P0,A	;HIEN THI HANG CHUC
	CLR P2.0	;CHON LED HANG CHUC
	SETB P2.1
	CALL DELAY5MS
	MOV P0,#0FFH
	RET
;CHUONG TRINH CON TAO TAN SO QUET LED
DELAY5MS:
	PUSH 00H	; CAT R0
	PUSH 01H	;CAT R1
	MOV R0,#50
RE_5MS:
	MOV R1,#100
	DJNZ R1,$
	DJNZ R0,RE_5MS
	POP 01H		;TRA LAI R1
	POP 00H		;TRA LAI R0
	RET
MA_LED:
	DB	0C0H,0F9H,0A4H,0B0H,099H,092H,082H,0F8H,080H,090H
	END