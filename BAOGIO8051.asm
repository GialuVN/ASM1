;;;;;;;;;;;;CHUONG TRINH DONG HO SO HEN GIO BAO;;;;;;;;;;;;;;
;''''''''''''VIET BOI LU MINH THI''''''''''''''''
;;;;;;;;;;;;;;;;;11/05/2009;;;;;;;;;;;;;;;;;;;;;;
;~~~~~~~~~~~defind~~~~~~~~~~~~~~
S	EQU	R5		;GIAY
M	EQU	R6		;PHUT
T	EQU R7		;GIO
          
P_SLE BIT P3.0	;CHON
P_INC BIT P3.1	;GIAM,12/24
BUZZ  BIT P3.2	;GIAM SAT NGUON
          
APM BIT 00H		;AM/PM
QUA	BIT	01H		;0.25 GIAY
EF1	BIT	03H		;CHO/KO PHEP CHOP PHUT
EF2	BIT	04H		;CHO/KO PHEP CHOP GIO
EF3	BIT 08H		;CHINH NGAY
T24	BIT	05H		;12/24
TEM5 BIT 06H	;BIEN TAM @
ASEC BIT 07H	;1 S
BUZX BIT 09H	; BIEN BAT CHUONG
COM	 BIT 0AH	;EX
          
TEM1 EQU  30H	;BIEN TAM CHO CHUONG TRINH HEXTODEC
TEM2 EQU  31H	;BIEN TAM CHO CHUONG TRINH DECTOSEV
TEM3 EQU  32H	;BIEN TAO XUNG 0.25S
TEM4 EQU  33H	;+++
H24  EQU  35H	;BIEN 24H
C5S	 EQU  36H	;BIEN DEM 5S
DAY	 EQU  37H	;BIEN NGAY
MINS EQU  38H	;BIEN PHUT
;~~~~~~~~BEGIN~~~~~~~~~~~~~~~~~~~
ORG		0H
JMP		MAIN
ORG		000BH
LJMP	SEC
MAIN:
;~~~~~~~INSTALL~~~~~~~~~~~~~~~~~~~
	MOV IE,#82H		;CHO PHEP NGAT BOI TIMER 0
	MOV TMOD,#11H	;TIMER 1, CHE DO 1
;~~~~~~~12H/AM~~~~~~~~~~~~~~~~~~~
	MOV S,#0
	MOV M,#0
	MOV T,#12		;12 GIO
	SETB APM		;SANG
	MOV DAY,#2		;THU 2
	MOV H24,#24		;24 GIO (CHE DO 24H)
	CLR T24			;TAT CHE DO 24H
;~~~~~~~ORTHER VARIABLE~~~~~~~~~
	MOV C5S,#0		;0 S
	CLR BUZX		;TAT CHUONG
	MOV R4,#0		;BIEN DEM TRONG CT NGAT
	MOV TEM3,#0		;0.25S
	CLR EF1			;KO CHOP
	CLR EF2			;KO CHOP
	MOV P3,#0FFH
	MOV R3,#0
	SETB COM
	SETB TF0
;~~~~~~~~CHUONG TRINH CHINH~~~~~
REPEAT:
	CLR EF1
	CLR EF2
	JNB P_SLE,SUBB_SLE
	JNB P_INC,SUBB_INC
	CALL DISP
	JMP REPEAT
;~~~~~~~~~NHAY NOI TIEP~~~~~~~~~~~~~~~~~~
SUBB_SLE:
		JMP SUB_SLE
SUBB_INC:
		JMP SUB_INC
;~~~~~~~~~~KEYBOARD~~~~~~~~~~~~~~~~~~~~~~~~
;;;;;;;;;;;DIEU CHINH PHUT;;;;;;;;;;;;;;;;;
SUB_SLE:
		SETB	EF1
		CLR		EF2
RE_SLE:
		CALL DISP
		JNB P_INC,SET_TIME
		JNB P_SLE,RE_SLE
RETURN1:
		JNB P_SLE,SUB1_SLE
		JNB P_INC,SUB1_INC
		CALL DISP
		MOV R0,#60
		CLR TEM5
		JMP RETURN1
;;;;;;;;;;;LOA;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
SET_TIME:
		CPL BUZZ
RE_SET:
		CALL DISP
		JNB P_INC,$-3
		JNB P_SLE,RE_SET
		JMP REPEAT
		
SUB1_INC:
		INC M
		CJNE M,#60,EX1_INC
		MOV M,#0
;~~~~~~~~~~~TANG CO (GIU)~~~~~~~~~~~~~~~~~~~~~~~
EX1_INC:
		JB P_INC,REPEAT_INC
		JB TEM5,REPEAT_DISP
REPEAT_INC:
		DJNZ R0,$+6
		SETB TEM5
		JMP SUB1_INC
		CALL DISP
		JNB P_INC,REPEAT_INC
		JMP RETURN1
		
REPEAT_DISP:
		PUSH 01H
		MOV R1,#5
DISP_RE:
		CALL DISP
		DJNZ R1,DISP_RE
		POP 01H
		JMP SUB1_INC		
;;;;;;;;;DIEU CHINH GIO;;;;;;;;;;;;;;;;;
SUB1_SLE:
		SETB 	EF2
		CLR		EF1
		CALL DISP
		JNB P_SLE,$-3
RETURN2:
		JNB P_SLE,SUB2_SLE
		JNB P_INC,SUB2_INC
		CALL DISP
		JMP RETURN2
SUB2_INC:
		INC T
		CJNE T,#12,TT_INC
		CPL APM
TT_INC:
		CJNE T,#13,EX2_INC
		MOV T,#1
EX2_INC:
		CALL DISP
		JNB P_INC,$-3
		JMP RETURN2
;;;;;;;;;;;;;;DIEU CHINH NGAY;;;;;;;;;;;;;;;;;
SUB2_SLE:
		SETB EF3
		CLR EF2
		CALL DISP
		JNB P_SLE,$-3
RETURN3:
		JNB P_SLE,SUB3_SLE
		JNB P_INC,SUB3_INC
		CALL DISP
		JMP RETURN3
SUB3_INC:
		INC DAY
		MOV A,DAY
		CJNE A,#9,EXIT3_INC
		MOV DAY,#2
EXIT3_INC:
		CALL DISP
		JNB P_INC,$-3
		JMP RETURN3
SUB3_SLE:
		CLR EF3
		CALL DISP
		JNB P_SLE,$-3
		JMP REPEAT
;;;;;;;;;CHE DO 24H/APM;;;;;;;;;;;;;;;;;;;;;;;
SUB_INC:
		CPL T24
		CALL DISP
		JNB P_INC,$-3
		JMP REPEAT
;~~~~~~~~~~~DISPLAY~~~~~~~~~~~~~~~~~~~~~~~~
DISP:
	JNB EF3,NOF3
	JNB QUA,EXIT_DISP3
NOF3:
	MOV TEM2,DAY
	CALL DECTOSEV
	MOV P1,TEM2
	CLR P0.4
	CALL DELAY2MS
	SETB P0.4
	MOV P1,#0FFH
EXIT_DISP3:
	JNB EF1,NOF1
	JB QUA,EXIT_DISP1
NOF1:
	MOV TEM1,M		;HIEN THI PHUT
	CALL HEXTODEC
	MOV A,TEM1
	ANL A,#0FH
	MOV TEM2,A
	CALL DECTOSEV
	MOV P1,TEM2
	
	JNB T24,TIP_T24
	MOV C,QUA
	MOV P1.7,C
	JMP EXIT_T24
TIP_T24:
	MOV C,APM
	MOV P1.7,C
EXIT_T24:
	CLR P0.3
	CALL DELAY2MS
	SETB P0.3
	MOV P1,#00H
	MOV A,TEM1
	SWAP A
	ANL A,#0FH
	MOV TEM2,A
	CALL DECTOSEV
	MOV P1,TEM2
	JNB ASEC,CROS1
	CLR P1.7
CROS1:	
	CLR P0.2
	CALL DELAY2MS
	SETB P0.2
	MOV P1,#00H
	JMP EXIT_M
EXIT_DISP1:
	CALL DELAY2MS
	CALL DELAY2MS
EXIT_M:
	JNB EF2,NOF2
	JB QUA,EXIT_DISP2
NOF2:
	JB T24,HT24H
	MOV TEM1,T		;HIEN THI GIO
	JMP HTAPM
HT24H:
	MOV TEM1,H24
HTAPM:
	CALL HEXTODEC
	MOV A,TEM1
	ANL A,#0FH
	MOV TEM2,A
	CALL DECTOSEV
	MOV P1,TEM2
	JNB ASEC,CROS2
	CLR P1.7
CROS2:
	CLR P0.1
	CALL DELAY2MS
	SETB P0.1
	MOV P1,#00H
	MOV A,TEM1
	SWAP A
	ANL A,#0FH
	MOV TEM2,A
	CALL DECTOSEV
	MOV P1,TEM2
	CLR P0.0
	CALL DELAY2MS
	SETB P0.0
	MOV P1,#00H
	JMP EXIT_T
EXIT_DISP2:
	CALL DELAY2MS
	CALL DELAY2MS
EXIT_T:
	MOV A,M
	CJNE A,MINS,EX_MIN
	JMP END_DIS
EX_MIN:
	MOV A,DAY
	CJNE A,#8,COMP
END_DIS:
	RET
;~~~~~~~~~~COMPARE~~~~~~~~~~~~~~~~~~~~~~~~~
COMP:
	JNB APM,APM_COM
	CJNE T,#6,COM1
	CJNE M,#45,COM1
	MOV MINS,#45
	SETB BUZX
COM1:
	CJNE T,#9,COM2
	CJNE M,#0,COM2
	MOV MINS,#0
	SETB BUZX
COM2:
	CJNE M,#15,COM3
	MOV MINS,#15
	SETB BUZX
COM3:
	CJNE T,#11,COM4
	CJNE M,#30,COM4
	MOV MINS,#30
	SETB BUZX
COM4:
	JMP END_COM
APM_COM:
	CJNE T,#12,COM5
	CJNE M,#45,COM5
	MOV MINS,#45
	SETB BUZX
COM5:
	CJNE T,#3,COM6
	CJNE M,#0,COM6
	MOV MINS,#0
	SETB BUZX
COM6:
	CJNE M,#15,COM7
	MOV MINS,#15
	SETB BUZX
COM7:
	CJNE T,#5,END_COM
	CJNE M,#30,END_COM
	MOV MINS,#30
	SETB BUZX
END_COM:
	JMP END_DIS
;~~~~~~~~~~DELAY2ms~~~~~~~~~~~~~~~~~~~~~~~~
DELAY2MS:
	PUSH 00H
	PUSH 01H
	MOV R0,#20
REPEAT_2MS:
	MOV R1,#100
	DJNZ R1,$
	DJNZ R0,REPEAT_2MS
	POP 01H
	POP 00H
	RET
;~~~~~~~~~GIAI MA HEX TO DEC~~~~~~~~~~~~~~~	
HEXTODEC:
	MOV A,TEM1
	MOV DPTR,#LIST1
	MOVC A,@A+DPTR
	MOV TEM1,A
	RET
;~~~~~~~~~~GIAI MA DEC TO 7 SEGMENT~~~~~~~~
DECTOSEV:
	MOV A,TEM2
	MOV DPTR,#LIST2
	MOVC A,@A+DPTR
	MOV TEM2,A
	RET
;~~~~~~~~CHUONG TRINH NGAT~~~~~~~~~~~~~~~~~
SEC:
	PUSH ACC
	CLR TR0
	MOV TL0,LOW(-49994)
	MOV TH0,HIGH(-49994)
	SETB TR0
	INC R4
	INC TEM3
	MOV A,TEM3
	CJNE A,#5,EXIT_QUA
	CPL QUA
	MOV TEM3,#0
EXIT_QUA:
	CJNE R4,#15,END_SEC
	MOV R4,#0
;~~~~~~~~~~~COUT/AM/PM/24~~~~~~~~~~~~~~~~~
COUT:
	CPL ASEC
	JNB BUZX,EX_C5S
	INC C5S
	MOV A,C5S
	CLR BUZZ
	CJNE A,#5,EX_C5S
	SETB BUZZ
	CLR BUZX
	MOV C5S,#0
EX_C5S:
	INC S
	CJNE S,#60,EXIT_SEC
	MOV S,#0
	INC M
	CJNE M,#60,EXIT_SEC
	MOV M,#0
	INC T
	CJNE T,#13,EXIT_SE
	MOV T,#1
	JMP EXIT_SEC
EXIT_SE:
	CJNE T,#12,EXIT_SEC
	CPL APM
	JNB APM,EXIT_SEC
	INC DAY
	MOV A,DAY
	CJNE A,#9,EXIT_SEC
	MOV DAY,#2
EXIT_SEC:
	JNB APM,CONV24
	CJNE T,#12,CONV224
	MOV A,T
	ADD A,#12
	MOV H24,A
	JMP END_SEC
CONV224:
	MOV H24,T
	JMP END_SEC
CONV24:
	CJNE T,#12,CONV225
	MOV H24,T
	JMP END_SEC
CONV225:
	MOV A,T
	ADD A,#12
	MOV H24,A
END_SEC:
	POP ACC
	RETI
;:::::::::::::::DATA-CODE;;;;;;;;;;;;;;;;;;;;;;;
LIST1:
	DB	00H,01H,02H,03H,04H,05H,06H,07H,08H,09H,10H,11H,12H,13H,14H,15H,16H,17H,18H
	DB	19H,20H,21H,22H,23H,24H,25H,26H,27H,28H,29H,30H,31H,32H,33H,34H,35H,36H,37H
	DB	38H,39H,40H,41H,42H,43H,44H,45H,46H,47H,48H,49H,50H,51H,52H,53H,54H,55H,56H
	DB	57H,58H,59H,60H
LIST2:
	DB	0C0H,0F9H,0A4H,0B0H,099H,092H,082H,0F8H,080H,090H  
END
 
	