;/////////CHUONG TRINH GIAI MA REMOTE TV SONY////////////
;//////////NGUOI VIET: LU MINH THI//////////////////////
;MO TA: CHUONG TRINH SU DUNG NGAT INT1/P3.3, MA 7 BIT CHUA TRONG BIT COM_
;
;
REC	BIT P3.3
;DINH NGHIA BIEN
COM_  DATA 30H
ADD_  DATA 31H
R8	  DATA 32H
R9    DATA 33H
R10   DATA 34H
R11   DATA 35H
R12   DATA 36H		
R13	  DATA 37H		;FREE
R14	  DATA 38H		;FREE
R15   DATA 39H		;FREE
TEM_  DATA 4AH

FLAG  DATA 20H
SLEC  BIT  FLAG.0
OKEY  BIT  FLAG.1
SIGN  BIT  FLAG.2


ORG 0000H
JMP MAIN
ORG 0013H
JMP INT_INT1
;KHOI TAO
MAIN:
		MOV IE,#84H
		SETB REC
		MOV COM_,#0
		MOV ADD_,#0
;BAT DAU CHUONG TRINH CHINH
SMAIN:	
		MOV A,COM_
		CJNE A,#80H,NU2
		CPL P1.0
		JMP COREC
NU2:
		CJNE A,#81H,NU3
		CPL P1.1
		JMP COREC
NU3:
		CJNE A,#82H,SMAIN
		CPL P1.2
COREC:
		MOV COM_,#0
		CALL TEST_RE
		JMP SMAIN
		
;CHUONG TRINH KIEM TRA SU LAP LAI		
TEST_RE:
		MOV R13,#255
REAP_TEST:
		MOV R12,#100
		DJNZ R12,$
		JNB REC,TEST_RE
		DJNZ R13,REAP_TEST
		SETB IE.7
		RET
;[[[[[[[[[[[[[[[[[[[[[[[[[[[[[[[[[[[[[[[[[[[[[[[[[[[[[[[[[[[[[[[[[[[[[
;$$$$$$$$$$$$$--CHUONG TRINH NGAT GIAI MA REMOTE SONY--$$$$$$$$$$$$$$$$
INT_INT1:
		MOV TEM_,COM_	
		MOV R9,#12
		MOV R8,#255
LAP4:
		NOP
		JB REC,EX_INT1				;;KIEM TRA BIT START
		DJNZ R8,LAP4				;VA CHONG NHIEU(BIT START <<2.4mS)
		JNB REC,$	         		;CHO DONG BO
		MOV R8,#200
LAP6:
		NOP
		NOP
		JB REC,CON_LAP6					
		JMP EX_LAP6
CON_LAP6:
		DJNZ R8,LAP6
		JMP EX_INT1
EX_LAP6:
		CLR OKEY					;CHUAN BI NHAN BIT DAU TIEN
		MOV ADD_,#255
		MOV COM_,#255
LAP3:
		CALL DELAY900X				;
		JNB REC,TIP				; XAC DINH BIT NHAN DC LA "0" HAY "1"
		CALL LGIC0	
		JMP CONT_TIP
TIP:
		CALL LGIC1
CONT_TIP:
		JNB OKEY,LAP3
		CLR IE.7				;NHAN XONG THI TAM DUNG_DOI XU LY!
		
EX_INT1:
		RETI
;==========NHAN BIT 0================
LGIC0:
		CLR SLEC
		CALL NAP
		INC R9
		DJNZ R9,CHECK_JAM0
		JMP EX_CHECK0
CHECK_JAM0:
		MOV R8,#200
L1_JAM0:		
		NOP
		NOP
		JB REC,EX_JAM0
		JMP EX_CHECK0
EX_JAM0:
		DJNZ R8,L1_JAM0
		SETB OKEY
		MOV COM_,TEM_
EX_CHECK0:
		RET
;========NHAN BIT 1==================
LGIC1:
		SETB SLEC
		CALL NAP
		JNB REC,$
		INC R9
		DJNZ R9,CHECK_JAM
		JMP EX_CHECK
CHECK_JAM:
		MOV R8,#200
L1_JAM:		
		NOP
		NOP
		JB REC,EX_JAM
		JMP EX_CHECK
EX_JAM:
		DJNZ R8,L1_JAM
		SETB OKEY
		MOV COM_,TEM_
EX_CHECK:
		RET
;========NHAN DU LIEU================
NAP:
		JB OKEY,CON_NAP
		CALL DICH
		DJNZ R9,CON_NAP
		MOV R10,#4
COMPL:
		CLR SLEC
		CALL DICH
		DJNZ R10,COMPL
		SETB OKEY
		MOV A,ADD_
		CJNE A,#0,ERRO
		MOV A,COM_
		MOV C,ACC.7
		JC  CON_NAP			;KIEM TRA LOI
ERRO:
		MOV COM_,TEM_
CON_NAP:
		RET
;=========DICH DU LIEU===========
DICH:
		MOV A,ADD_
		MOV C,SLEC
		RRC A
		MOV ADD_,A
		MOV A,COM_
		RRC A
		MOV COM_,A
		RET
;=====CHUONG TRINH TRE 0.9mS=====
DELAY900X:
		MOV R11,#4
L_DELAY900X:		
		MOV R8,#100
		DJNZ R8,$
		DJNZ R11,L_DELAY900X
		RET
;]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]]

END