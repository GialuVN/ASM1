;;;;;;;;;;;;CHUONG TRINH DONG HO SO;;;;;;;;;;;;;;
;''''''''''''VIET BOI LU MINH THI''''''''''''''''
;;;;;;;;;;;;;;;;;11/05/2009;;;;;;;;;;;;;;;;;;;;;;
;~~~~~~~~~~~defind~~~~~~~~~~~~~~
S	EQU	R5		;GIAY
M	EQU	R6		;PHUT
T	EQU R7		;GIO
D	EQU	P2		;THU

P_SLE BIT P3.0	;CHON
P_INC BIT P3.1	;TANG,AM/PM
H2    BIT P3.3	;24/12
AP    BIT P3.4	;AM(1)/PM(0)
DSE	  BIT P3.5	;HIEN THI GIAY

APM BIT 00H		;AM/PM
QUA	BIT	01H		;0.25 GIAY
EF1	BIT	03H		;CHO/KO PHEP CHOP PHUT
EF2	BIT	04H		;CHO/KO PHEP CHOP GIO
T24	BIT	05H		;12/24

TEM1 EQU  30H	;BIEN TAM CHO CHUONG TRINH HEXTODEC
TEM2 EQU  31H	;BIEN TAM CHO CHUONG TRINH DECTOSEV
TEM3 EQU  32H	;BIEN TAO XUNG 0.25S
TEM4 EQU  33H	;BIEN THU TU
DAY	 EQU  34H	;BIEN THU
H24  EQU  35H	;BIEN 24H
C5S	 EQU  36H	;BIEN DEM 5S
;~~~~~~~~BEGIN~~~~~~~~~~~~~~~~~~~
ORG		0000H
JMP		MAIN
ORG		000BH
JMP		SEC
MAIN:
;~~~~~~~INSTAL~~~~~~~~~~~~~~~~~~~
	MOV IE,#82H		;CHO PHEP NGAT BOI TIMER 0
	MOV TMOD,#01H	;TIMER 0, CHE DO 1
;~~~~~~~12H/AM~~~~~~~~~~~~~~~~~~~
	MOV S,#0
	MOV M,#0
	MOV T,#12		;12 GIO
	SETB APM		;SANG
	MOV H24,#24		;24 GIO (CHE DO 24H)
	MOV DAY,#0FEH	;THU 2
	CLR T24			;TAT CHE DO 24H
	SETB H2			;KO HT CHE DO 24H
;~~~~~~~ORTHER VARIABLE~~~~~~~~~
	MOV C5S,#0		;0 S
	MOV R4,#0		;BIEN DEM TRONG CT NGAT
	MOV TEM3,#0		;0.25S
	MOV TEM4,#0		;MAC DINH
	CLR EF1			;KO CHOP
	CLR EF2			;KO CHOP
	SETB TF0
;~~~~~~~~CHUONG TRINH CHINH~~~~~
REPEAT:
	JNB P_SLE,SUBB_SLE
	JNB P_DEC,SUBB_DEC
	JNB P_INC,SUBB_INC
	CALL DISP
	JMP REPEAT
;~~~~~~~~~NHAY NOI TIEP~~~~~~~~~~~~~~~~~~
SUBB_SLE:
		JMP SUB_SLE
SUBB_DEC:
		JMP SUB_DEC
SUBB_INC:
		JMP SUB_INC
	
;~~~~~~~~~~KEYBOARD~~~~~~~~~~~~~~~~~~~~~~~~
;;;;;;;;;;;DIEU CHINH PHUT;;;;;;;;;;;;;;;;;
SUB_SLE:
		SETB	EF1
		CLR		EF2
		CALL DISP
		JNB P_SLE,$-3
RETURN1:
		JNB P_SLE,SUB1_SLE
		JNB P_DEC,SUB1_DEC
		JNB P_INC,SUB1_INC
		CALL DISP
		JMP RETURN1
SUB1_DEC:
		DEC M
		CJNE M,#-1,EX1_DEC
		MOV M,#59
EX1_DEC:
		CALL DISP
		JNB P_DEC,$-3
		JMP RETURN1
		
SUB1_INC:
		INC M
		CJNE M,#60,EX1_INC
		MOV M,#0
EX1_INC:
		CALL DISP
		JNB P_INC,$-3
		JMP RETURN1
;;;;;;;;;DIEU CHINH GIO;;;;;;;;;;;;;;;;;
SUB1_SLE:
		SETB 	EF2
		CLR		EF1
		CALL DISP
		JNB P_SLE,$-3
RETURN2:
		JNB P_SLE,SUB2_SLE
		JNB P_DEC,SUB2_DEC
		JNB P_INC,SUB2_INC
		CALL DISP
		JMP RETURN2
SUB2_DEC:
		DEC T
		CJNE T,#11,TT_DEC
		CPL APM
TT_DEC:
		CJNE T,#0,EX2_DEC
		MOV T,#12
EX2_DEC:
		CALL DISP
		JNB P_DEC,$-3
		JMP RETURN2
		
SUB2_INC:
		INC T
		CJNE T,#12,TT_INC
		CPL APM
TT_INC:
		CJNE T,#13,EX2_INC
		MOV T,#1
EX2_INC:
		CALL DISP
		JNB P_INC,$-3
		JMP RETURN2
;;;;;;;;;;;;CHINH THU;;;;;;;;;;;;;;;;;;;;;

SUB2_SLE:
		CLR EF1
		CLR EF2
		CALL DISP
		JNB P_SLE,$-3
RETURN3:
		JB QUA,CONT3
		MOV D,DAY
		JMP CONT4
CONT3:
		MOV D,#0FFH
CONT4:
		JNB P_SLE,SUB3_SLE
		JNB P_DEC,SUB3_DEC
		JNB P_INC,SUB3_INC
		CALL DISP
		JMP RETURN3
SUB3_DEC:
		MOV A,DAY
		RR A
		MOV DAY,A
		MOV D,A
		CALL DISP
		JNB P_DEC,$-3
		JMP RETURN3
SUB3_INC:
		MOV A,DAY
		RL A
		MOV DAY,A
		MOV D,A
		CALL DISP
		JNB P_INC,$-3
		JMP RETURN3
SUB3_SLE:
		CALL DISP
		JNB P_SLE,$-3
		JMP REPEAT
;;;;;;;;;;;SANG/CHIEU;;;;;;;;;;;;;;;;;;;;;;
SUB_DEC:
		CPL APM
		CALL DISP
		JNB P_DEC,$-3
		JMP REPEAT
;;;;;;;;;CHE DO 24H;;;;;;;;;;;;;;;;;;;;;;;
SUB_INC:
		CPL T24
		CALL DISP
		JNB P_INC,$-3
		JMP REPEAT
;~~~~~~~~~~~DISPLAY~~~~~~~~~~~~~~~~~~~~~~~~
DISP:
	JNB EF1,NOF1
	JB QUA,EXIT_DISP1
NOF1:
	MOV TEM1,M		;HIEN THI PHUT
	CALL HEXTODEC
	MOV A,TEM1
	ANL A,#0FH
	MOV TEM2,A
	CALL DECTOSEV
	MOV P1,TEM2
	MOV P0,#07H
	CALL DELAY2MS
	MOV P0,#0FFH
	MOV P1,#00H
	MOV A,TEM1
	SWAP A
	ANL A,#0FH
	MOV TEM2,A
	CALL DECTOSEV
	MOV P1,TEM2	
	MOV P0,#0BH
	CALL DELAY2MS
	MOV P0,#0FFH
	MOV P1,#00H
EXIT_DISP1:
	JNB EF2,NOF2
	JB QUA,EXIT_DISP2
NOF2:
	JB T24,HT24H
	MOV TEM1,T		;HIEN THI GIO
	JMP HTAPM
HT24H:
	MOV TEM1,H24
HTAPM:
	CALL HEXTODEC
	MOV A,TEM1
	ANL A,#0FH
	MOV TEM2,A
	CALL DECTOSEV
	MOV P1,TEM2
	MOV P0,#0DH
	CALL DELAY2MS
	MOV P0,#0FFH
	MOV P1,#00H
	MOV A,TEM1
	SWAP A
	ANL A,#0FH
	MOV TEM2,A
	CALL DECTOSEV
	MOV P1,TEM2	
	MOV P0,#0EH
	CALL DELAY2MS
	MOV P0,#0FFH
	MOV P1,#00H
EXIT_DISP2:
	JNB T24,EXIT_T24
	CLR H2
	JMP MOD24
EXIT_T24:
	SETB H2
MOD24:	
	MOV C,APM
	MOV AP,C
	MOV D,DAY
;+++++++++VITRI THUONG TRUC++++++++++++++++
	RET
;~~~~~~~~~~DELAY2ms~~~~~~~~~~~~~~~~~~~~~~~~
DELAY2MS:
	PUSH 00H
	PUSH 01H
	MOV R0,#20
REPEAT_2MS:
	MOV R1,#100
	DJNZ R1,$
	DJNZ R0,REPEAT_2MS
	POP 01H
	POP 00H
	RET
;~~~~~~~~~GIAI MA HEX TO DEC~~~~~~~~~~~~~~~	
HEXTODEC:
	MOV A,TEM1
	MOV DPTR,#LIST1
	MOVC A,@A+DPTR
	MOV TEM1,A
	RET
;~~~~~~~~~~GIAI MA DEC TO 7 SEGMENT~~~~~~~~
DECTOSEV:
	MOV A,TEM2
	MOV DPTR,#LIST2
	MOVC A,@A+DPTR
	MOV TEM2,A
	RET
;~~~~~~~~CHUONG TRINH NGAT~~~~~~~~~~~~~~~~~
SEC:
	PUSH ACC
	MOV TL0,LOW(-49740)
	MOV TH0,HIGH(-49740)
	SETB TR0
	INC R4
	INC TEM3
	MOV A,TEM3
	CJNE A,#5,$+7
	CPL QUA
	MOV TEM3,#0
	CJNE R4,#20,EXIT_SEC
	CPL DSE
	INC C5S
	MOV A,C5S
	CJNE A,#5,EX_C5S
	CPL P3.6
	MOV C5S,#0
EX_C5S:
	MOV R4,#0
;~~~~~~~~~~~~~~~~~DEM~~~~~~~~~~~~~~~~~~~~~	
	INC S
	CJNE S,#60,EXIT_SEC
	MOV S,#0
	INC M
	CJNE M,#60,EXIT_SEC
	MOV M,#0
	INC T
	CJNE T,#13,EXIT_SE
	MOV T,#1
	JMP EXIT_SEC
EXIT_SE:
	CJNE T,#12,EXIT_SEC
	CPL APM
	JNB APM,EXIT_SEC
	MOV A,DAY
	RL A
	MOV DAY,A
	MOV D,DAY
EXIT_SEC:
	CALL CONVERT
	POP ACC
	RETI
;~~~~~~~~~~CONVERT AM/PM-24H~~~~~~~~~~~~~~
CONVERT:
	JNB APM,CONV24
	CJNE T,#12,CONV224
	MOV A,T
	ADD A,#12
	MOV H24,A
	JMP EXCONV
CONV224:
	MOV H24,T
	JMP EXCONV
CONV24:
	CJNE T,#12,CONV225
	MOV H24,T
	JMP EXCONV
CONV225:
	MOV A,T
	ADD A,#12
	MOV H24,A
EXCONV:
	RET
LIST1:
	DB	00H,01H,02H,03H,04H,05H,06H,07H,08H,09H,10H,11H,12H,13H,14H,15H,16H,17H,18H
	DB	19H,20H,21H,22H,23H,24H,25H,26H,27H,28H,29H,30H,31H,32H,33H,34H,35H,36H,37H
	DB	38H,39H,40H,41H,42H,43H,44H,45H,46H,47H,48H,49H,50H,51H,52H,53H,54H,55H,56H
	DB	57H,58H,59H,60H
LIST2:
	DB	3FH,06H,5BH,4FH,66H,6DH,7DH,07H,7FH,6FH
END

	
	
	