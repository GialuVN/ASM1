;=====DEFINITE=========
LED	EQU	P1		;XUAT RA LED
KY1	BIT	P3.4	;PHIM 1
KY2	BIT P3.5	;PHIM 2
LRM BIT P3.7	;DEN NGU 12H-6H SANG
;=====VARIANT BYTE=====
HOR DATA 22H	;12H
MIN DATA 23H	;<>
SEC DATA 24H	;<>
X_HOR DATA 25H	;24H
_SEC   DATA 60H
_QUA   DATA 61H
_HAF   DATA 62H
_FUL   DATA 63H
;=====SHOW CODES ======
XX0 DATA 2DH
XX1 DATA 2EH
XX2 DATA 2FH
XX3 DATA 30H
XX4 DATA 31H
XX5 DATA 32H
XX6 DATA 33H
XX7 DATA 34H
HO1 DATA 35H
HO2 DATA 36H
SU1 DATA 37H
MI1 DATA 38H
MI2 DATA 39H
SU2 DATA 3AH
SE1 DATA 3BH
SE2 DATA 3CH
SP1 DATA 3DH
SP2 DATA 3EH
SP3 DATA 3FH
SP4 DATA 40H
SP5 DATA 41H
SP6 DATA 42H
SP7 DATA 43H
SP8 DATA 44H
SP9 DATA 45H
SP10 DATA 46H
SP11 DATA 47H
SP12 DATA 48H
SP13 DATA 49H
;======TEMPERATE=======
TEM1 DATA 5AH
TEM2 DATA 5BH
TEM3 DATA 5CH
;=====VARIANT BIT======
BITS DATA 20H
APM BIT BITS.0	;AM/PM
INT BIT BITS.1	;BAO NGAT
H24	BIT	BITS.2	;12/24H
QUA BIT BITS.3	;1/4 GIAY
HAF BIT BITS.4	;1/2 GIAY
FUL BIT BITS.5	;1 GIAY
BHO BIT	BITS.6	;CHON GIO
BMI	BIT BITS.7	;CHON PHUT
SLEEP DATA 21H
SLE BIT SLEEP.0 ;DEN NGU
MIS BIT SLEEP.1 ;THOI GIAN HIEN GIAY/PHUT
EMN BIT SLEEP.2 ;BAT/TAT HIEN PHUT/GIAY
QU2 BIT SLEEP.3 ;1/4
KIP BIT SLEEP.4	;TANG CO GIU 
;+++++++++++++++++++++++++++++
ORG 0000H
JMP MAIN
ORG 0001BH
JMP INT_0
MAIN:
;==========INITIAL===========
CALL DELAY
MOV HOR,#12		;12 GIO
MOV MIN,#0		;0 PHUT
MOV SEC,#0		;0 GIAY
MOV BITS,#0
SETB SLE		;BAT DEN NGU
SETB APM 		;BUOI SANG
SETB H24		;CHO DO 12H
MOV P3,#0FFH	;CHO P3 LEN MUC CAO
MOV TMOD,#10H	;BO DINH THOI 0 CHE DO 1
MOV IE,#88H		;CHO PHEP NGAT DO TIMER 0
MOV R0,#0
MOV R1,#0
MOV R2,#0
MOV R3,#0
MOV R4,#30
MOV R5,#10
MOV R6,#0
MOV R7,#0
MOV TEM1,#2DH
MOV TEM2,#0
SETB TR1
SETB TF1
MOV _SEC,#0
MOV _QUA,#0
MOV _HAF,#0
MOV _FUL,#0
BEGIN:	
		JNB KY1,H24_12
		JNB KY2,SET_TIME
		CALL GEN
		JMP BEGIN
;=======TONG HOP==========
GEN:
		CALL DISP
		RET
;===CHON CHE DO 24H-12H===
H24_12:
		MOV R6,#0
		CPL H24
		MOV R3,#30
REP_H24_12:
		CALL GEN
		JB KY1,END_H24_12
		DJNZ R3,REP_H24_12
		CPL H24
		CPL SLE
		JNB SLE,CONT_SLE
		MOV R3,#255
		CLR LRM
REP_LRM:
		CALL COUNT_SEC
		CALL CONTV_24H
		CALL DELAY
		CALL DELAY
		DJNZ R3,REP_LRM
		SETB LRM
		JMP END_H24_12
CONT_SLE:
		MOV R3,#30
		MOV R4,#5
REP_CONT_SLE:
		CALL GEN
		DJNZ R3,REP_CONT_SLE
		MOV R3,#30
		CPL LRM
		DJNZ R4,REP_CONT_SLE
		SETB LRM
END_H24_12:
		CALL GEN
		JNB KY1,$-2
		JMP BEGIN
;======TINH CHINH=========
;CHINH PHUT
SET_TIME:
		SETB EMN
		SETB MIS
		SETB BMI
		CLR BHO
		CALL GEN
		JNB KY2,$-2
RETURN:
		CALL GEN
		JNB KY1,INC_MIN
		JNB KY2,SET_HOR
		JMP RETURN
INC_MIN:
		INC MIN
		MOV A,MIN
		CJNE A,#60,REP_KIP
		MOV MIN,#0
;CHINH PHUT CO GIU
REP_KIP:
		CALL GEN
		DJNZ R5,REP_KIP
		MOV R5,#10
		CLR BMI
		JB KY1,CONT_REP
		JB KIP,INC_MIN
		DJNZ R4,CONT_REP
		MOV R4,#30
		SETB KIP
		JMP INC_MIN
CONT_REP:
		JNB KY1,REP_KIP
		MOV R4,#30
		MOV R5,#10
		CLR KIP
		SETB BMI
RE_GEN:
		CALL GEN
		SETB HAF
		MOV R1,#0
		JNB KY1,RE_GEN
		JMP RETURN
;CHINH GIO
SET_HOR:
		SETB BHO
		CLR BMI
		CALL GEN
		JNB KY2,$-2
RETURN2:
		CALL GEN
		JNB KY1,INC_HOR
		JNB KY2,SET_LRM
		JMP RETURN2
INC_HOR:
		INC HOR
		MOV A,HOR
		CJNE A,#12,CONT_INC_HOR
		CPL APM
CONT_INC_HOR:
		CJNE A,#13,END_INC_HOR
		MOV HOR,#1		
END_INC_HOR:
		CALL GEN
		SETB HAF
		MOV R1,#0
		JNB KY1,END_INC_HOR
		JMP RETURN2
;TRO VE
SET_LRM:
		CLR EMN
		CLR BMI
		CLR BHO
		CPL LRM
		CALL GEN
		JNB KY2,$-2
		MOV R6,#0
		JMP BEGIN
;=======DEM GIAY==========
COUNT_SEC:
		JNB INT,END_FUL
		INC _SEC
		MOV A,_SEC
		CJNE A,#5,CONT_QUA
		MOV _SEC,#0
		CPL QUA
		SETB QU2
CONT_QUA:
		INC _QUA
		MOV A,_QUA
		CJNE A,#10,CONT_HAF
		MOV _QUA,#0
		CPL HAF				;PHAT XUNG NUA GIAY
CONT_HAF:
		INC _HAF
		MOV A,_HAF
		CJNE A,#20,END_FUL
		MOV _HAF,#0
		CPL FUL				;PHAT XUNG 1 GIAY
		CALL COUNT_TIME
		INC _FUL
		MOV A,_FUL
		CJNE A,#10,END_FUL
		MOV _FUL,#0
		JB EMN,END_FUL
		CPL MIS
END_FUL:
		CLR INT
		RET
;=====CHIA THOI GIAN=======
COUNT_TIME:
		INC SEC
		MOV A,SEC
		CJNE A,#60,END_C
		MOV SEC,#0
		INC MIN
		MOV A,MIN
		CJNE A,#60,END_C
		MOV MIN,#0
		INC HOR
		MOV A,HOR
		CJNE A,#12,CONT_C
		CPL APM
CONT_C:
		CJNE A,#13,END_C
		MOV HOR,#1
END_C:
		RET
;=====CHUYEN DOI 12H-24H======
CONTV_24H:
		JNB APM,CONT_CV
		MOV A,HOR
		CJNE A,#12,CONT1_CV
		MOV X_HOR,#24
		JMP END_CV
CONT1_CV:
		MOV X_HOR,HOR
		JMP END_CV
CONT_CV:
		MOV A,HOR
		CJNE A,#12,CONT3_CV
		MOV X_HOR,#12
		JMP END_CV
CONT3_CV:
		ADD A,#12
		MOV X_HOR,A
END_CV:
		RET
;==========HIEN THI==========
DISP:
;PHUT
		JNB BMI,END_BMI
		JB HAF,END_BMI
		CALL DELAY
		CALL DELAY
		JMP END1_HAF
END_BMI:	
		MOV A,MIN
		CALL H_TO_D
		PUSH ACC
		ANL A,#0FH
		CALL D_TO_7
		MOV MI2,A
		MOV P1,A
		JB H24,END_QUA
		JNB QUA,CONT2_QUA
		CPL P1.7
		JMP CONT2_QUA
END_QUA:
		JNB APM,CONT_NIG
		SETB P1.7
		JMP CONT2_QUA
CONT_NIG:
		CLR P1.7
CONT2_QUA:
		CLR P3.3
		CALL DELAY
		SETB P3.3
		POP ACC
		SWAP A
		ANL A,#0FH
		CALL D_TO_7
		MOV MI1,A
		MOV P1,A
		JNB FUL,$+4
		CLR P1.7
		CLR P3.2
		CALL DELAY
		SETB P3.2
END1_HAF:
;GIO	
		JNB BHO,END_BHO
		JB HAF,END_BHO
		CALL DELAY
		CALL DELAY
		JMP END2_HAF
END_BHO:
		JB H24,CONT_12
		MOV A,X_HOR
		JMP CONT_24
CONT_12:
		MOV A,HOR
CONT_24:
		CALL H_TO_D
		PUSH ACC
		ANL A,#0FH
		CALL D_TO_7
		MOV HO2,A
		MOV P1,A
		JNB FUL,$+4
		CLR P1.7
		CLR P3.1
		CALL DELAY
		SETB P3.1
		POP ACC
		SWAP A
		ANL A,#0FH
		CALL D_TO_7
		MOV HO1,A
		MOV P1,A
		CLR P3.0
		CALL DELAY
		SETB P3.0
;DEN NGU 24H-6H
END2_HAF:
		JNB SLE,END_DISP
		MOV A,X_HOR
		CJNE A,#24,CONT_COM
		CLR LRM
CONT_COM:
		CJNE A,#6,END_DISP
		SETB LRM
END_DISP:
		JB MIS,END_MIS
		CALL SHOW
END_MIS:
		RET
;============TRINH DIEN=============
SHOW:	
		CALL CONVERT
		MOV R0,TEM1
REP_SHOW:
		JNB KY1,END_SHOW
		JNB KY2,END_SHOW
		MOV R6,#0
		CALL COUNT_SEC
		MOV P1,@R0
		CALL SWEEP
		INC R0
		INC TEM2
		MOV A,TEM2
		CJNE A,#9,REP_SHOW
		MOV TEM2,#0
		MOV R0,TEM1
		JNB QU2,SHOW
		CLR QU2
		INC TEM1
		MOV A,TEM1
		CJNE A,#47H,SHOW
		MOV TEM1,#2DH
		MOV TEM2,#0
		SETB MIS
END_SHOW:
		MOV R6,#0
		SETB MIS
		CALL GEN
		JNB KY1,END_SHOW
		JNB KY2,END_SHOW
		RET
;==============SWEEP================
SWEEP:	
		MOV A,TEM2
		CJNE A,#0,CONT_SW1
		CLR P2.0
		CALL DELAY
		SETB P2.0
		JMP END_SWEEP
CONT_SW1:
		MOV A,TEM2
		CJNE A,#1,CONT_SW2
		CLR P2.1
		CALL DELAY
		SETB P2.1
		JMP END_SWEEP
CONT_SW2:
		MOV A,TEM2
		CJNE A,#2,CONT_SW3
		CLR P2.2
		CALL DELAY
		SETB P2.2
		JMP END_SWEEP
CONT_SW3:
		MOV A,TEM2
		CJNE A,#3,END_SWEEP
		CLR P2.3
		CALL DELAY
		SETB P2.3
END_SWEEP:
		RET
;=============CONTVERT==============
CONVERT:
		MOV SU1,#0BFH
		MOV SU2,#0BFH
		MOV A,SEC
		CALL H_TO_D
		PUSH ACC
		ANL A,#0FH
		CALL D_TO_7
		MOV SE2,A
		POP ACC
		SWAP A
		ANL A,#0FH
		CALL D_TO_7
		MOV SE1,A
;FOOTER
		MOV SP1,#0FFH
		MOV SP2,#092H
		MOV SP3,#086H
		MOV SP4,#0C6H
		MOV SP5,#0FEH
		MOV SP6,#0BFH
		MOV SP7,#0F7H
		MOV SP8,#0BFH
		MOV SP9,#0FEH
		MOV SP10,HO1
		MOV SP11,HO2
		MOV SP12,MI1
		MOV SP13,MI2
;HEADER
		MOV XX0,HO1
		MOV XX1,HO2
		MOV XX2,MI1
		MOV XX3,MI2
		MOV XX4,#0FEH
		MOV XX5,#0BFH
		MOV XX6,#0F7H
		MOV XX7,#0BFH
		RET
;======GIAI MA DEC SANG 7 DOAN======
D_TO_7:
		MOV DPTR,#CODE7
		MOVC A,@A+DPTR
		RET
;=====GIAI MA HEX SANG DEC==========
H_TO_D:
		MOV DPTR,#CODEX
		MOVC A,@A+DPTR
		RET
;===========TRI HOAN 1mS============
DELAY:
		PUSH 0
		PUSH 1
		MOV R0,#10
REP_DELAY:
		MOV R1,#100
		DJNZ R1,$
		DJNZ R0,REP_DELAY
		POP 1
		POP 0
		RET
;=========TAO XUNG CHUAN 50mS========
INT_0:
		MOV TH1,#HIGH(-49987)
		MOV TL1,#LOW(-49987)
		SETB INT
		CALL COUNT_SEC
		CALL CONTV_24H
		RETI
CODE7:
	DB	0C0H,0F9H,0A4H,0B0H,099H,092H,082H,0F8H,080H,090H,0FFH
CODEX:
	DB	00H,01H,02H,03H,04H,05H,06H,07H,08H,09H,10H,11H,12H,13H,14H,15H,16H,17H,18H
	DB	19H,20H,21H,22H,23H,24H,25H,26H,27H,28H,29H,30H,31H,32H,33H,34H,35H,36H,37H
	DB	38H,39H,40H,41H,42H,43H,44H,45H,46H,47H,48H,49H,50H,51H,52H,53H,54H,55H,56H
	DB	57H,58H,59H,60H
END







