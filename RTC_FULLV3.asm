_DS		BIT		P1.7
SH_CP	BIT		P1.6
ST_CP	BIT		P1.5
KEY1	BIT		P1.0
KEY2	BIT		P1.1
KEY3	BIT		P1.2
;============================
;RTC DS1307
SCL BIT P3.4
SDA	BIT P3.5
;SQW BIT P1.2
;---------------------------
WIRTE	EQU		0D0H
READ	EQU		0D1H
;---------------------------

_SEC	EQU		27H				;GIAY
_MIN	EQU		28H				;PHUT
_HOUR	EQU		29H				;GIO
_DAY	EQU		2AH				;THU
_DATE	EQU 	2BH				;NGAY
_MONT	EQU		2CH				;THANG
_YEAR	EQU		2DH				;NAM
_CONT	EQU		2EH				;


_TEMP	EQU		2FH				;TAM
;-----------------------
TEMP	DATA	30H
ADDR	DATA	31H
BYTES	DATA	32H
INDEX	DATA	33H
;================================

R8		DATA	40H
R9		DATA	41H
R10		DATA	42H
R11		DATA	43H
;----------------------------------
C100MS	DATA	44H
C250MS	DATA	45H
C500MS 	DATA	46H
C1S		DATA	47H
C10S	DATA	48H
;================================
;BIEN HIEN TH
TEP1	DATA	50H
TEP2	DATA	51H

YEA1	DATA	52H
YEA2	DATA	53H
MON1	DATA	54H
MON2	DATA	55H
DAT1	DATA	56H
DAT2	DATA	57H
DAY1	DATA	58H
DAY2	DATA	59H
SEC1	DATA	5AH
SEC2	DATA	5BH
MIN1	DATA	5CH
MIN2	DATA	5DH
HOR1	DATA	5EH
HOR2	DATA	5FH
;===================================
;BIEN TAM
T_SEC	EQU		60H
T_MIN	EQU		61H
T_HOR	EQU		62H
T_DAY	EQU		63H
T_DAT	EQU		64H
T_MON	EQU		65H
T_YEA	EQU		66H

BCD		DATA	67H
HEX		DATA	68H
;===================================
;DINH NGHIA BIT
FLAGS	DATA	22H
F1S		BIT		FLAGS.7
FR		BIT		FLAGS.6
FY		BIT		FLAGS.5
FG		BIT		FLAGS.4
NC1		BIT		FLAGS.3
NC2		BIT		FLAGS.2
NC3		BIT		FLAGS.1
NC4		BIT		FLAGS.0
;-----------------------------------
TEM1 	DATA 	20H

T50MS  BIT  TEM1.0
T250MS BIT  TEM1.1
T500MS BIT  TEM1.2
T1S	   BIT  TEM1.3
T10S   BIT  TEM1.4
T100MS BIT	TEM1.5
;-----------------------------------
RTC		DATA	21H
CH_		BIT		RTC.3			;BAT TAT DAO DONG 0--BAT,1--TAT
H12_24	BIT		RTC.2			;CHON CHE DO 12H OR 24H 0-- 24H, 1--12H
APM		BIT		RTC.1			; APM=1--PM, 0--AM
SETT	BIT		RTC.0			;CHON CHE DO CAI DAT/HIEN THI	CAI DAT---1, HT---0
;-----------------------------------
MASK 	DATA	23H

MAS1	BIT		MASK.0
MAS2	BIT		MASK.1
MAS3	BIT		MASK.2
MAS4	BIT		MASK.3	
MAS5	BIT		MASK.4
MAS6	BIT		MASK.5	
MAS7	BIT		MASK.6	
	
;-----------------------------------	

ORG 	0000H
JMP		BEGIN
ORG		001BH
MOV TH1,#HIGH(-50000)
MOV TL1,#LOW (-50000)
CLR TF1
SETB T50MS
RETI

;===================================================
BEGIN:
MOV IE,#88H				;CHO PHEP NGAT DO BO DINH THOI 1
MOV TMOD,#10H			; TIMER 1 CHE DO 16BIT
SETB TF1
SETB TR1

MOV TEM1,#255

MOV _SEC,#12H
MOV _MIN,#34H
MOV _HOUR,#09H
MOV _DAY,#07H
MOV _DATE,#12H
MOV _MONT,#10H
MOV _YEAR,#87H


CALL CVH_DATA
CALL CVB_DATA

MOV C100MS,#2
MOV C250MS,#5
MOV C500MS,#2
MOV C1S,#2
MOV C10S,#10

MOV MASK,#0
MOV FLAGS,#00000000B
SETB SETT

SETB KEY1
SETB KEY2
SETB KEY3
	
SETB SCL
SETB SDA

CLR _DS
CLR SH_CP
CLR ST_CP

MOV _TEMP,#36
CALL DE_TEMP





MOV MASK,#255



;======================================================
MAIN:	
		CALL CVB_DATA
		CALL HIEN_THI
		CALL TIMING
		JMP MAIN
		
		
		CALL F_DISP
		CALL HIEN_THI
		CALL TIMING
		JNB KEY1,CV1
		JMP MAIN
CV1:
		MOV A,_HOUR
		CPL ACC.6
		MOV _HOUR,A
		CPL SETT
		MOV BYTES,#01H
		MOV ADDR,#02H
		MOV INDEX,#_HOUR
		CALL MASTER_W
L_KEY1:
		CALL HIEN_THI
		CALL TIMING
		JNB KEY1,L_KEY1
		JMP MAIN
SETTING:
		
		CALL CVB_DATA
		CALL HIEN_THI


		RET
			
;----------HEX TO BCD-------------
CVB_DATA:
		MOV A,T_SEC
		CALL HEX2BCD
		JNB MAS1,CORSS1
		JNB T500MS,EX_MAS1
CORSS1:		
		MOV SEC2,A
		MOV SEC1,B
		JMP EX_MAS11
EX_MAS1:
		MOV SEC2,#10
		MOV SEC1,#10
EX_MAS11:
		MOV A,T_MIN
		CALL HEX2BCD
		JNB MAS2,CORSS2
		JNB T500MS,EX_MAS2
CORSS2:		
		MOV MIN2,A
		MOV MIN1,B
		JMP EX_MAS22
EX_MAS2:		
		MOV MIN2,#10
		MOV MIN1,#10
EX_MAS22:
		MOV A,T_HOR
		CALL HEX2BCD
		JNB MAS3,CORSS3
		JNB T500MS,EX_MAS3
CORSS3:		
		MOV HOR2,A
		MOV HOR1,B	
		JMP EX_MAS33
EX_MAS3:		
		MOV HOR2,#10
		MOV HOR1,#10
EX_MAS33:		
		MOV A,T_DAY
		CALL HEX2BCD
		JNB MAS4,CORSS4
		JNB T500MS,EX_MAS4
CORSS4:				
		MOV DAY2,A
		MOV DAY1,B
		JMP EX_MAS44
EX_MAS4:		
		MOV DAY2,#10
		MOV DAY1,#10
EX_MAS44:		
		MOV A,T_DAT
		CALL HEX2BCD
		JNB MAS5,CORSS5
		JNB T500MS,EX_MAS5
CORSS5:				
		MOV DAT2,A
		MOV DAT1,B
		JMP EX_MAS55
EX_MAS5:		
		MOV DAT2,#10
		MOV DAT1,#10
EX_MAS55:		
		MOV A,T_MON
		CALL HEX2BCD
		JNB MAS6,CORSS6
		JNB T500MS,EX_MAS6
CORSS6:				
		MOV MON2,A
		MOV MON1,B
		JMP EX_MAS66
EX_MAS6:		
		MOV MON2,#10
		MOV MON1,#10
EX_MAS66:		
		MOV A,T_YEA
		CALL HEX2BCD

		JNB MAS7,CORSS7
		JNB T500MS,EX_MAS7
CORSS7:				
		MOV YEA2,A
		MOV YEA1,B
		JMP EX_MAS77
EX_MAS7:		
		MOV YEA2,#10
		MOV YEA1,#10
EX_MAS77:		
		MOV R0,#52H
L_CVB_DATA:		
		MOV A,@R0
		CALL DECODE_SEG
		MOV @R0,A
		INC R0
		CJNE R0,#60H,L_CVB_DATA
		RET
;----------BCD TO HEX--------------
CVH_DATA:
		MOV A,_SEC
		CLR ACC.7
		MOV _SEC,A
		MOV A,_HOUR
		JNB H12_24,CONT1_ADJ_DATA
		ANL A,#00011111B
		JMP CONT2_ADJ_DATA
CONT1_ADJ_DATA:			
		ANL A,#00111111B	
CONT2_ADJ_DATA:
		MOV _HOUR,A
		MOV R0,#_SEC
		MOV R1,#T_SEC
L_CVH_DATA:
		MOV BCD,@R0
		CALL BCD2HEX
		MOV @R1,HEX
		INC R0
		INC R1
		CJNE R0,#(_SEC+8),L_CVH_DATA
		RET
;-----------FUNCTION HEX TO BCD-------------
HEX2BCD:
		MOV B,#10
		DIV AB
			RET
;----------FUNCTION BCD TO HEX--------------			
BCD2HEX:
		MOV A,BCD
		SWAP A
		ANL A,#00001111B
		CJNE A,#0,CONT_BCD2HEX
		MOV HEX,BCD
		JMP EX_BCD2HEX
CONT_BCD2HEX:
		MOV A,BCD
CONT3_BCD2HEX:		
		CLR C
		SUBB A,#6
		MOV HEX,A
		MOV B,#10
		DIV AB
		SWAP A
		ORL A,B
		CJNE A,BCD,CONT2_BCD2HEX
		JMP EX_BCD2HEX		
CONT2_BCD2HEX:
		MOV A,HEX
		JMP CONT3_BCD2HEX		
EX_BCD2HEX:
		RET
;----------GIAI MA GIAY------------		
DE_SEC:
		MOV A,_SEC
		ANL A,#00001111B
		CALL DECODE_SEG
		MOV SEC1,A
		MOV A,_SEC
		SWAP A
		ANL A,#00000111B
		CALL DECODE_SEG
		MOV SEC2,A
		RET
;------------GIAI MA PHUT----------
DE_MIN:

		MOV A,_MIN
		ANL A,#00001111B
		CALL DECODE_SEG
		MOV MIN1,A
		MOV A,_MIN
		SWAP A
		ANL A,#00000111B
		CALL DECODE_SEG
		MOV MIN2,A
		RET
;--------GIAI MA GIO---------------
DE_HOR:
		MOV A,_HOUR
		ANL A,#00001111B
		CALL DECODE_SEG
		MOV HOR1,A
		MOV A,_HOUR
		SWAP A
		JNB H12_24,HR1
		ANL A,#00000001B
		JMP HR3
HR1:
		ANL A,#00000011B
HR3:
		CALL DECODE_SEG
		MOV HOR2,A
		RET
;----------GIAI MA THU-------------		
DE_DAY:
		MOV A,_DAY
		CALL DECODE_SEG
		MOV DAY1,A
		MOV DAY2,#3FH	;SO 0
		RET
;------------GIAI MA NGAY----------
DE_DATE:
		MOV A,_DATE
		ANL A,#00001111B
		CALL DECODE_SEG
		MOV DAT1,A
		MOV A,_DATE
		SWAP A
		ANL A,#00001111B
		CALL DECODE_SEG
		MOV DAT2,A
		RET
;----------GIAI MA THANG-----------
DE_MONT:
		MOV A,_MONT
		ANL A,#00001111B
		CALL DECODE_SEG
		MOV MON1,A
		MOV A,_MONT
		SWAP A
		ANL A,#00001111B
		CALL DECODE_SEG
		MOV MON2,A
		RET
;-----------GIAI MA NAM-------------
DE_YEAR:
		MOV A,_YEAR
		ANL A,#00001111B
		CALL DECODE_SEG
		MOV YEA1,A
		MOV A,_YEAR
		SWAP A
		ANL A,#00001111B
		CALL DECODE_SEG
		MOV YEA2,A
		RET
;-----------GIAI MA NHIET DO----------
DE_TEMP:
		MOV A,_TEMP
		MOV B,#10
		DIV AB
		CALL DECODE_SEG
		MOV TEP2,A
		MOV A,B
		CALL DECODE_SEG
		MOV TEP1,A		
		RET
;--------------GIAI MA FULL-------------
DE_ALL:
		CALL GIVE_FLAG
		CALL DE_SEC
		CALL DE_MIN
		CALL DE_HOR
		CALL DE_DAY
		CALL DE_DATE
		CALL DE_MONT
		CALL DE_YEAR
		RET
;-------------HIEN THI CO APM 24/12------
F_DISP:
		JNB H12_24,CON_FF1
		CLR FY
		SETB FG
		JMP	CON_FF2
CON_FF1:
		SETB FY
		CLR FG
CON_FF2:
		MOV C,APM
		MOV FR,C
		RET	
;------------LAY GIO 24/12 AND APM------		

GIVE_FLAG:
		MOV A,_HOUR
		MOV C,ACC.6
		MOV H12_24,C
		JNB H12_24,CONT_GIVE_FLAG
		MOV C,ACC.5
		MOV APM,C
		JMP EX_GIVE_FLAG
CONT_GIVE_FLAG:		
		CLR APM
EX_GIVE_FLAG:
		RET

;===============================================		

		
;GIAI MA BCD - LED 7 DOAN
DECODE_SEG:
		MOV DPTR,#MALED
		MOVC A,@A+DPTR
		RET
		
;---------------------------------------------------		
;DOC DU LIEU TU DS1307 VOI CAC THAM SO: BYTES(SO BYTES DUOC DOC LIEN TIEP),INDEX(DIA CHI DICH 8051),ADDR(DIA CHI NGUON DS1307 )	
MASTER_R:
		PUSH 00H
		PUSH 07H
		CALL SELECT_ADDR
		CALL CON_S
		MOV TEMP,#READ
		CALL __BYTE_WIRTE	
		MOV R7,BYTES
		MOV R0,INDEX
__LOOP4:
		CALL ACK
		CALL __BYTE_READ
		MOV @R0,TEMP
		INC R0
		DJNZ R7,__LOOP4
		CALL N_ACK
		CALL CON_P
		POP 07H
		POP 00H
		RET
;GHI DU LIEU LEN DS1307 VOI CAC THAM SO: BYTES(SO BYTES DUOC GHI LIEN TIEP),INDEX(DIA CHI DU LIEU NGUON 8051),ADDR(DIA CHI DICH DS1307)
MASTER_W:
		PUSH 07H
		PUSH 00H
		CALL CON_S
		MOV TEMP,#WIRTE
		CALL __BYTE_WIRTE
		CALL ACK
		MOV TEMP,ADDR
		CALL __BYTE_WIRTE
		CALL ACK
		MOV R7,BYTES
		MOV R0,INDEX
LOOP3__:
		MOV A,@R0
		INC R0
		MOV TEMP,A
		CALL __BYTE_WIRTE
		CALL ACK
		DJNZ R7,LOOP3__
		CALL CON_P
		POP 00H
		POP 07H
		RET
;DINH CON TRO DU LIEU QUA ADDR		
SELECT_ADDR:
		CALL CON_S
		MOV TEMP,#WIRTE
		CALL __BYTE_WIRTE
		CALL ACK
		MOV TEMP,ADDR
		CALL __BYTE_WIRTE
		CALL ACK
		CALL CON_P
		RET
;PHAT 1BYTE DU LIEU TRONG TEMP

__BYTE_WIRTE:
		PUSH 07H
		MOV A,TEMP
		MOV R7,#8
LOOP1__:
		RLC A
		MOV SDA,C
		SETB SCL
		CALL DELAYP
		CLR SCL
		DJNZ R7,LOOP1__
		POP 07H
		RET
; NHAN 1 BYTE DU LIEU CAT VAO TEMP
__BYTE_READ:
		PUSH 07H
		MOV R7,#8
LOOP2__:	
		SETB SCL
		MOV C,SDA
		CALL DELAYP
		CLR SCL
		RLC A
		DJNZ R7,LOOP2__
		MOV TEMP,A
		POP 07H
		RET
; DIEU KIEN START
CON_S:
		SETB SDA
		SETB SCL
		CALL DELAYN
		CLR SDA
		CALL DELAYN
		CLR SCL
		RET
;DIEU KIEN STOP
CON_P:
		CLR SDA
		CLR SCL
		CALL DELAYN
		SETB SCL
		CALL DELAYN
		SETB SDA
		RET
;BAO DA NHAN/PHAT 1 BYTE
ACK:
		CLR SCL
		CLR SDA
		CALL DELAYP
		SETB SCL
		CALL DELAYP
		CLR SCL
		SETB SDA
		CALL DELAYP
		RET
;DAO CUA ACK
N_ACK:
		CLR SCL
		SETB SDA
		CALL DELAYP
		SETB SCL
		CALL DELAYP
		RET
		
;SHORT DELAY
DELAYP:
		NOP
		NOP
		RET
DELAYN:
		CALL DELAYP
		CALL DELAYP
		RET		
;--------------------------------------------		
;CHUONG TRINH HIEN THI (VIP)
HIEN_THI:
		MOV R0,#50H		;DIA CHI CUA SO CUOI CUNG
		MOV R8,#1
		MOV R9,#7		;SO DOAN CUA LED
LOOP_HT:
		MOV A,FLAGS
		MOV R10,#8		
LOOP_HTX:		
		MOV C,ACC.0
		MOV _DS,C
		SETB SH_CP
		CLR SH_CP
		RR A
		DJNZ R10,LOOP_HTX
		MOV R10,#16
LOOP_HT1:	
		MOV A,@R0
		MOV B,R8
		DIV AB
		MOV C,ACC.0
		MOV _DS,C
		SETB  SH_CP
		CLR   SH_CP
		INC R0
		DJNZ R10,LOOP_HT1
		MOV R0,#50H
		MOV R10,#8
		MOV A,R8
LOOP_HT2:
		MOV C,ACC.7
		MOV _DS,C
		SETB SH_CP
		CLR SH_CP
		RL A
		DJNZ R10,LOOP_HT2
		SETB ST_CP
		CLR  ST_CP
		JB SETT,EX_T1S
		JNB T1S,EX_T1S
		CLR T1S
		MOV BYTES,#7
		MOV ADDR,#00H
		MOV INDEX,#_SEC
		CALL MASTER_R
		CALL DE_ALL
		JMP EX_T1S2
EX_T1S:	
		CALL DELAY2MS
EX_T1S2:

		MOV A,R8
		RL A
		MOV R8,A
		DJNZ R9,LOOP_HT
		RET	
;-------------------------------------	
;CAC TIMER
TIMING:
		JNB T50MS,EX_TIMING
		CLR T50MS
		DJNZ C100MS,EX_100MS
		CPL T100MS
		MOV C100MS,#2
EX_100MS:
		DJNZ C250MS,EX_TIMING
		CPL T250MS
		MOV C250MS,#5
		DJNZ C500MS,EX_TIMING
		CPL T500MS
		CPL F1S
		MOV C500MS,#2
		DJNZ C1S,EX_TIMING
		CPL T1S
		MOV C1S,#2
		DJNZ C10S,EX_TIMING
		CPL T10S
		MOV C10S,#10
EX_TIMING:
		RET
;============CHUONG TRINH DELAY==========
DELAY2MS:
		MOV 7FH,#10
L_DELAY:
		MOV 7EH,#100
		DJNZ 7EH,$
		DJNZ 7FH,L_DELAY
		RET
;============CHUONG TRINH DELAY2==========
DELAY50MS:
		MOV 7FH,#100
L_DELAY50:
		MOV 7EH,#250
		DJNZ 7EH,$
		DJNZ 7FH,L_DELAY50
		RET



MALED:
	DB 03Fh,06h,05Bh,04Fh,066h,06Dh,07Dh,07h,07Fh,06Fh,00H
;C
;N
;A
;B
;C
;D
;E
;F


END
