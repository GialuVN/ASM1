;KHAI BAO
RS BIT P3.0
RW BIT P3.1
E  BIT P3.2
KT BIT P1.7
ROW DATA 30H
EXP EQU P1
XX BIT 20H
ORG	0
MAIN:
	MOV ROW,#1
	SETB E		;DAT CHAN E LEN CAO
	MOV DPTR,#DULIEU
	MOV TMOD,#01H	;TIMER 1 CHE DO 2
	MOV A,#38H	;KHOI TAO LCD
	CALL COMMAND
	MOV A,#0CH	;BAT HIEN THI,TAT CON TRO
	CALL COMMAND
	
	MOV R3,#13
LAPXX:
	PUSH DPL
	PUSH DPH
	MOV R2,#4
LAPX:	
	CALL X_ROW
	CALL DISP_CHAR
	CALL INC_DPTR
	DJNZ R2,LAPX
	CALL DELAY
	POP DPH
	POP DPL
	CALL INC_DPTR
	MOV A,#1
	CALL COMMAND
	DJNZ R3,LAPXX
	CPL XX
	JMP MAIN
	
;HIEN THI CHUOI KI TU
DISP_CHAR:
	MOV R0,#0
LOOP2:
	MOV A,R0
	MOVC A,@A+DPTR
	CALL DISPLAY
	INC R0
	CJNE R0,#16,LOOP2
	RET
;CHON DONG
X_ROW:
	CALL C_ROW
	INC ROW
	MOV A,ROW
	CJNE A,#5,CONT
	MOV ROW,#1
	CONT:
	RET
C_ROW:
	PUSH 01H
	MOV A,ROW
	CJNE A,#1,ROW1
	MOV R1,#80H	;COT 1
ROW1:
	CJNE A,#2,ROW2
	MOV R1,#0C0H	;COT 2
ROW2:
	CJNE A,#3,ROW3
	MOV R1,#90H	;COT 3
ROW3:
	CJNE A,#4,END_ROW
	MOV R1,#0D0H	;COT 4
END_ROW:
	MOV A,R1
	POP 01H
	CALL COMMAND
	RET
	
;CHUONG TRINH CON RA LENH CHO LCD
COMMAND:
	CALL READY
	MOV EXP,A ;DUA DU LIEU RA P1
	CLR RS	; RS=0	LCD NHAN LENH
	CLR RW	; RW=0	GHI LENH LEN LCD
	SETB E	;CHAN E TU CAO XUONG THAP
	CLR E
	RET
;CHUONG TRINH CON HIEN THI RA LCD
DISPLAY:
	CALL READY
	MOV EXP,A
	SETB RS	;RS=1 CHO PHEP HIEN THI
	CLR RW	;RW=0 GHI KI TU LEN LCD
	SETB E ;CHAN E TU CAO XUONG THAP
	CLR E ;
;CHUONG TRINH CON KIEN TRA CO BAN CUA LCD
READY:
	SETB KT	;DAT BIT P1.7 LAM NGO VAO
	CLR RS	;RS=0 DOC THANH GHI LENH
	SETB RW	;R\W=1 DOC THANH GHI LENH
REPEAT:
	CLR E	;CHAN E TU CAO XUONG THAP
	SETB E	;
	JB KT,REPEAT
	SETB E	;TRA CHAN E VE MUC CAO
	RET
;CHUONG TRINH TRE
DELAY:
	PUSH 00H
	MOV R0,#10
LOOP:
	MOV TH0,#HIGH(-50000)
	MOV TL0,#LOW(-50000)
	SETB TR0
	JNB TF0,$
	CLR TR0
	CLR TF0
	DJNZ R0,LOOP
	POP 00H
	RET
;TANG DPTR 16 LAN
INC_DPTR:
	PUSH 00H
	MOV R0,#16
REP_INC:
	INC DPTR
	DJNZ R0,REP_INC
	POP 00H
	RET
DULIEU:	
	DB '                '
	DB '                '
	DB '                '
	DB '                '
	DB '  XOA MAN HINH  '
	DB 'TRO VE DAU DONG '
	DB '  TANG CON TRO  '
	DB ' DICH SANG TRAI '
	DB '  BAT HIEN THI  '
	DB '    NHAP NHAY   '
	DB ' CHAY LEN XUONG '
	DB ' DUNG YEN CHOP  '
	DB ' CHAY CHAY CHOP '
	END
	
